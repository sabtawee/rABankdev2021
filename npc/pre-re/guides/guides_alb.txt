//===== eAthena Script ======================================= 
//= Alberta Guides
//===== By: ================================================== 
//= kobra_k88; L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.5a
//===== Compatible With: ===================================== 
//= eAthena  SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Guides for the city of Alberta
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed Armory coords, thanks to Freya team
//= 1.2 Optimized, updated common guide names [Lupus]
//= 1.3 Updated the guide according to how it is on iRO, thanks [MasterOfMuppets]
//=     to Muad_Dib
//= 1.4 Fitted the many lines into 1 or 2, and corrected some typos [erKURITA]
//= 1.5 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//= 1.5 Fixed some errors with the loop[Samuray22]
//	-Thanks to $ephiroth.
//============================================================ 

alberta,23,238,4	script	Guide#alb::AlbGuide	105,{
	cutin "prt_soldier",2;
	mes "[Alberta Guide]";
	mes "ยินดีต้อนรับสู่ Alberta";
	mes "เมืองท่าเรือ";
	mes "สอบถามข้าได้ตลอดเวลาหากเจ้าต้องการ";
	mes "ทราบตำแหน่งสถานที่ในเมืองนี้";
	mes "วิธีดูเจ้าลองเลือก 'ประกาศ'";
	set .@loop1,1;
	while (.@loop1) {
		next;
		switch(select("แนะนำเมือง:ลบเครื่องหมายออกจาก Mini-Map:ประกาศ:ยกเลิก")) {
		case 1:
			mes "[Alberta Guide]";
			mes "กรุณาเลือก";
			mes "สถานที่ที่ต้องการดู";
			mes "จากรายการเมนู";
			if (.@compass_check == 0) {
				mes "เจ้าต้องการให้ข้า";
				mes "ทำเครื่องหมายบน";
				mes "Mini-Map ให้หรือเปล่า";
				next;
				if (select("ต้องการ:ยกเลิก") == 1) set .@compass_check,1;
			}
			set .@loop2,1;
			while (.@loop2) {
				if (.@wait_button_chk == 0) set .@wait_button_chk,1;
				else next;
				
				switch(select("^FF0000สมาคมพ่อค้า^000000:ร้านขายอาวุธ:ร้านขายอุปกรณ์ทั่วไป:โรงแรม:ห้องหลอม:ยกเลิก")) {
				case 1:
					mes "[Alberta Guide]";
					mes "สมาคมพ่อค้า";
					mes "สามารถเปลี่ยน";
					mes "อาชีพเป็นพ่อค้าได้ที่นั่น";
					mes "ตั้งอยู่";
					mes "ทิศตะวันตกเฉียงใต้";
					mes "ของ Alberta";
					if (.@compass_check)
						viewpoint 1,33,41,2,0xFF0000;
					break;
				case 2:
					mes "[Alberta Guide]";
					mes "ร้านขายอาวุธ";
					mes "สามารถค้นพบอยู่ใน";
					mes "ทิศใต้ของ Alberta.";
					if (.@compass_check)
						viewpoint 1,117,37,3,0xFF00FF;
					break;
				case 3:
					mes "[Alberta Guide]";
					mes "ร้านขายอุปกรณ์";
					mes "อยู่ตรงกลางของ Alberta";
					if (.@compass_check)
						viewpoint 1,98,154,4,0xFF00FF;
					break;
				case 4:
					mes "[Alberta Guide]";
					mes "โรงแรมอยู่ทางนั้น";
					mes "ที่ทางเหนือ";
					mes "ของ Alberta";
					if (.@compass_check)
						viewpoint 1,65,233,5,0xFF00FF;
					break;
				case 5:
					mes "[Alberta Guide]";
					mes "ห้องหลามของ Alberta";
					mes "อยู่ในตึกเดียวกับ";
					mes "สมาคมพ่อค้า";
					mes "ขึ้นไปทางข้างบนห้อง";
					if (.@compass_check)
						viewpoint 1,35,41,6,0xFF00FF;
					break;
				case 6:
					mes "[Alberta Guide]";
					mes "กรุณาถามข้าถ้าต้องการ ''ลบ";
					mes "เครื่องหมายบน Mini-Map'' ถ้าเจ้า";
					mes "ไม่ต้องการให้เครื่องหมาย";
					mes "อยู่บนหน้าจอของเจ้า";
					mes "ในหน้าต่าง Mini-Map";
					set .@loop2,0;
				}
			}
			break;
		case 2:
			viewpoint 2,237,41,2,0xFF0000;
			viewpoint 2,237,41,3,0xFF00FF;
			viewpoint 2,46,345,4,0xFF00FF;
			viewpoint 2,175,220,5,0xFF00FF;
			viewpoint 2,175,220,6,0xFF00FF;
			set .@compass_check,0;
			break;
		case 3:
			mes "[Alberta Guide]";
			mes "ใช้เทคโนโลยีขั้นสูง";
			mes "โปรดพิสูจน์ด้วยตาของเจ้า";
			mes "ข้าจะทำเครื่องหมายให้เจ้า";
			mes "ในตำแหน่งของ Mini-Map";
			mes "เพื่อนำทางให้เจ้า";
			next;
			mes "[Alberta Guide]";
			mes "ถ้าไม่เจอแผนที่ย่อ";
			mes "ให้เจ้ากดปุ่ม Ctrl+Tab";
			mes "หรือคลิกปุ่มแผนที่บนหน้าต่างตัวละคร";
			next;
			mes "[Alberta Guide]";
			mes "เจ้าสามารถคลิก ''+'' และ ''-''";
			mes "เป็นสัญลักษณ์เพื่อย่อขยาย";
			mes "อยู่ตรง Mini-Map";
			break;
		case 4:
			mes "[Alberta Guide]";
			mes "ขอให้เจ้าเดินทางโดยสวัสดิภาพ";
			close2;
			set .@loop1,0;
		}
	}
	cutin "prt_soldier",255;
	end;
}

alberta,120,60,3	duplicate(AlbGuide)	Guide#2alb	105