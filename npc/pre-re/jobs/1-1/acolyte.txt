// -- # Ragnarok Online EP 4.0 BY Kelberwitz Blade's
// -- # Acolyte Job Change
// -- # =============================================

prt_church,184,41,4	script	Cleric#Aco01	60,{
	
	if (Upper == 1) {
	if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
	if (Class == Job_Novice_High) {
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "อ่า, ข้ารู้สึกได้ถึงความพรากเพียรของเจ้า";
		mes " ประสบการณ์ที่ผ่านมาคงได้สอนอะไรๆ หลายๆ อย่างให้เจ้าแล้วใช่มั้ย";
		next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "^0066FF[ Father Mareusis ]^000000";
				mes " ข้าว่าเจ้ากลับไปเรียน Basic Skill ให้ครบก่อนดีกว่านะ";
				next;
					mes "^0066FF[ Father Mareusis ]^000000";
					mes " ข้าไม่อยากให้เสียเวลา";
					mes " เอาเป็นว่าพร้อมเมื่อไหร่ก็กลับมาหาข้า";
					mes " แล้วข้าจะรอเจ้า.";
					close;
				}
		mes "^0066FF[ Father Mareusis ]^000000";
		mes " เยี่ยม, ยินดีต้อนรับการกลับมาของเจ้า ข้าว่าเจ้าคงจะพร้อมแล้วสินะที่จะไปเผชิญโลกใหม่อีกครั้ง";
		next;
			skill 143,0,0;
			jobchange Job_Acolyte_High;
			skill 156,1,0;
			mes "^0066FF[ Father Mareusis ]^000000";
			mes " ขณะนี้มีผู้คนมากมายที่ต้องการความช่วยเหลือจากเจ้า เจ้าจงไปรับใช้แทนพระผู้เป็นเจ้าด้วยเถิด";
			close;
		} else {
			mes "^0066FF[ Father Mareusis ]^000000";
			mes " ข้าคิดว่าเจาคงจะใช้คำสอนของพระผู้เป็นเจ้าไปในทางที่ถูกต้องนะ";
			close;
			}
		} else {
			mes "^0066FF[ Father Mareusis ]^000000";
			mes " ความพรากเพียรของเจ้าที่ผ่านมา คงจะทำให้เจ้าเรียนรู้และมีประสบการณ์มากขึ้นนะ";
			next;
			mes "^0066FF[ Father Mareusis ]^000000";
			mes " เจ้าจงเดินหาเส้นทางที่เหมาะสมของเจ้าเอง";
			close;
		}
	}
	mes "^0066FF[ Father Mareusis ]^000000";
	mes " เจ้ามาหาข้าเพราะเหตุใดรึ ?";
	next;
	switch(select("^ff0000^000000 ข้าต้องการเป็น  Acolyte:^ff0000^000000 ข้าอยากรู้รายละเอียด:^ff0000^000000 ข้าแค่เดินผ่านมาเฉย เฉย ~")) {
	case 1:
		mes "^0066FF[ Father Mareusis ]^000000";
		if (BaseJob == Job_Acolyte) {
			mes " นี่ไม่ใช่เวลาเล่นตลกนะ!!";
			mes " เจ้าก็เป็น Acolyte อยู่แล้วนี่!";
			close;
		}
		else if (BaseJob != Job_Novice) {
			mes " ข้าเสียใจ , แต่เจ้าไม่ใช่ Novice ..";
			mes "ข้าไม่สามารถทำตามขอของเจ้าได้";
			close;
		}
		if (job_acolyte_q == 0) {
			mes " นี่คงเป็นสิ่งที่เจ้าต้องการแล้วใช่มั้ย?";
			mes " เจ้าต้องการเป็น..";
			mes " ผู้รับใช้ของพระเจ้าหรือไม่?";
			next;
			if (select("^ff0000^000000 ใช่.. นี่คือสิ่งที่ข้าต้องการ:^ff0000^000000 ไม่! ข้าโกหกท่าน") == 1) {
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "สวัสดี , ^FF0000"+strcharinfo(0)+" ^000000"; 
				mes "ก่อนที่เจ้าจะเป็น Acolyte ต้องได้รับการฝึกฝน";
				mes "จากพระผู้เป็นเจ้าเสียก่อนนะ..";
				next;
				mes "^0066FF[ Father Mareusis ]^000000";
				mes " เอาล่ะ..";
				mes " ข้าจะมอบภารกิจให้กับเจ้า";
				switch(rand(3)) {
				default:
					set job_acolyte_q,2;
					mes " เจ้าจงไปหา ^000077บาทหลวง Rubalkabara^000000 เพื่อที่เจ้าจะต้องไปแสวงบุญและบำเพ็ญตบะใกล้ๆ ^000077เมือง Prontera ทางตะวันออกเฉียงเหนือของเมือง^000000 และหลังจากนั้นเจ้าจงกลับมาหาอีกครั้ง";
					setquest 1001;
					break;
				case 1:
					set job_acolyte_q,3;
					mes " เจ้าจงไปหา ^000077แม่ชี Mathilda^000000 เพื่อที่เจ้าจะต้องไปแสวงบุญและบำเพ็ญตบะใกล้ๆ ^000077เมือง Morocc ตะวันตกเฉียงใต้ของเมือง Prontera^000000 และหลังจากนั้นเจ้าจงกลับมาหาอีกครั้ง";
					setquest 1002;
					break;
				case 2:
					set job_acolyte_q,4;
					mes " เจ้าจงไปหา ^000077บาทหลวง Yosuke^000000 เพื่อที่เจ้าจะต้องไปแสวงบุญและบำเพ็ญตบะรอบๆ ^000077สะพานทางตะวันตกเฉียงเหนือของเมือง  Prontera^000000 และหลังจากนั้นเจ้าจงกลับมาหาอีกครั้ง";
					setquest 1003;
					break;
				}
				next;
				mes "^0066FF[ Father Mareusis ]^000000";
				mes " พระผู้เป็นเจ้าจะนำทางให้กับเจ้าในระหว่างที่เจ้าเดินทาง";
				close;
			}
			mes "^0066FF[ Father Mareusis ]^000000";
			mes " นี่เจ้ามาเพราะเรื่องแค่นี้เองหรอ?";
			close;
		}
		mes " โอ้.. เจ้ากลับมาแล้ว ข้าขอตรวจสอบดูหน่อยว่าเจ้าพร้อมที่จะเป็นผู้รับใช้พระผู้เป็นเจ้าหรือยัง..";
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		if (getskilllv("NV_BASIC") < 9) {
			mes "ท่านยังเรียน Basic Skill ไม่ถึงระบบ 9 เลย ! มันจำเป็นมาก";
			next;
			mes "^0066FF[ Father Mareusis ]^000000";
			mes "ท่านยังต้องฝึกมากกว่านี้! กลับไปเรียน Basic จนกว่าจะถึงระดับ 9";
			close;
		}
		if (job_acolyte_q < 5) {
			mes "หืม ? ทำไมข้ายังไม่เห็นท่านในรายชื่อผู้สำเร็จการเดินทางหละ";
			next;
			switch(job_acolyte_q) {
			case 2:
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "จงไปพบ ^000077Father Rubalkabara^000000, และกลับมาที่นี่เขาจะอยู่บริเวณ ^000077ทิศตะวันออกเฉียงเหนือของ Prontera^000000.";
				break;
			case 3:
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "จงไปพบ ^000077Mother Mathilda^000000 และกลับมาที่นี่เขาจะอยู่บริเวณ ^000077ทะเลทรายแห่งเมือง Morroc^000000.";
				break;
			case 4:
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "จงไปพบ ^000077Father Yosuke^000000 และกลับมาที่นี่เขาจะอยู่บริเวณ ^000077สะพานซักแห่งทางตะวันตกเฉียงเหนือของ Prontera^000000.";
				break;
			}
			next;
			mes "^0066FF[ Father Mareusis ]^000000";
			mes "ขอให้พระเจ้า .. นำทางเจ้าและกลับมาโดยปลอดภัย";
			close;
		}
		mes "อืมม ...";
		mes "ชื่อของเจ้าปรากฏในรายชื่อผู้สำเร็จการเดินทาง";
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "ยินดี! ยินดีด้วยที่เจ้าจะเป็น Acolyte ณ บัดนี้";
		next;
		skill 143,0,0;
		callfunc "Job_Change",Job_Acolyte;
		callfunc "F_ClearJobVar";
		if(checkquest(1001) != -1) {
			completequest 1001;
		}
		else if(checkquest(1002) != -1) {
			completequest 1002;
		}
		else {
			completequest 1003;
		}
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "อย่าลืมขอบคุณพระเจ้าผู้ซึ่งช่วยเหลือเราตลอดเวลา";
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "และใช้พลังแห่งพระเจ้าช่วยเหลือผู้อื่น. ในเวลาที่มืดมนจงเชื่อในศรัทธาแห่งพระเจ้า";
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "สุดท้ายนี้ข้ายินดีที่ท่านผ่านการทดสอบอันยากเย็นและขอให้ท่านจงยึดมั่นในความดีตลอดไป";
		close;
	case 2:
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "เจ้าหวังที่จะได้เป็น Acolyte ใช่ไหม? เจ้าจะต้องมีครบข้อกำหนด 2 อย่าง";
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "อย่างแรก, เจ้าจะต้องมี Job Level 9 และเรียนรู้ Basic Skill ทั้งหมด,อย่างที่สอง เจ้าจะต้องผ่านการทดสอบเสียก่อน";
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		if (job_acolyte_q != 0) {
			switch(job_acolyte_q) {
			case 2:
				mes "จงไปพบกับ^000077Father Rubalkabara ^000000 และกลับมาหาพ่อที่นี่.";
				next;
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "เขาจะบำเพ็ญเพียรอยู่^000077ทางตะวันออกเฉียงเหนือของเมือง Prontera^000000.";
				break;
			case 3:
				mes "จงไปพบกับ^000077Mother Mathilda^000000 และกลับมาหาพ่อที่นี่";
				next;
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "เธอจะบำเพ็ญเพียรอยู่ไกล้^000077Morroc, ที่อยู่ทางตะวันตกเฉียงใต้ของเมือง Prontera^000000.";
				break;
			default:
				mes "จงไปพบกับ^000077Father Yosuke^000000 และกลับมาหาพ่อที่นี่";
				next;
				mes "^0066FF[ Father Mareusis ]^000000";
				mes "เขาจะบำเพ็ญเพียรอยู่รอบๆสะพาน^000077ทางตะวันตกเฉียงเหนือของเมือง Prontera^000000.";
				break;
			}
			next;
			mes "^0066FF[ Father Mareusis ]^000000";
			mes "อาจเป็นแสงแห่งพระเจ้านำทางให้เจ้าได้แสวงหาการบำเพ็ญเพียร";
		}
		else {
			mes "การทดสอบนี้จะตัดสินว่าลูกควรจะกรอกแบบฟอร์มนี่ไหม";
		}
		next;
		mes "^0066FF[ Father Mareusis ]^000000";
		mes "โปรดกลับมาหลังจากปฏิบัติตามข้อกำหนดทั้งสองอย่างแล้วที่พ่อบอกเจ้า ตราบใดที่เจ้ารับใช้พระเจ้า เจ้าจะสามารถทำมันได้";
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Ascetic#aco	89,{
	mes "^0066FF[ Father Rubalkabara ]^000000";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "ขอให้ท่านเดินทางอย่างปลอดภัย เมื่อท่านถึงโบสถ์แห่งพรอนเทร่าเขาจะรู้ว่าท่านมาพบข้าแล้ว";
			next;
			mes "^0066FF[ Father Rubalkabara ]^000000";
			mes "ข้าจะส่งพิราบสื่อสารไปหาท่านพ่อ หวังว่ามันจะถึงที่นั่นโดยปลอดภัย";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "หืม? ท่านนั่นเองที่คุณพ่อพูดถึง ข้าได้รับจดหมายจากโบทถ์ในขณะที่ท่านกำลังเดินทางมา";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "ชื่อของเจ้าคือ " + strcharinfo(0) + ", ถูกต้องไหม? ดีมากที่เจ้ามาได้ถูกทาง";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "ข้าเชื่อว่าเจ้าน่าจะเรียนรู้อะไรมากมายจากท่าน Mareusis แล้ว";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "และไม่คิดว่าข้าต้องสอนอะไรมากมาย เพราะว่าดูท่าทางเจ้าจะต้องลองพิสูจน์ด้วยตนเองดีกว่ามาฟังคนแก่อย่างข้า ฮ่า ๆ ~";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "งั้น..ตอนนี้เจ้าสามารถกลับไปยังโบสถ์ได้แล้วข้าจะส่งข้อความไปให้ถึงก่อนท่าน";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "โชคดี";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "โอ้ววว.. เจ้าคือหนึ่งใน";
				mes "คนที่จะเป็น Acolyte ใช่ไหม..?";
				mes "ไหนดูซิ...";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "เจ้าชื่อ " + strcharinfo(0) + "?";
				mes "ข้าไม่คิดว่ามี";
				mes "ชื่อนี้ในลิสต์ของข้า หืม.....";
				next;
				mes "^0066FF[ Father Rubalkabara ]^000000";
				mes "ทำไมเจ้าไม่กลับไปที่ Prontera และตรวจสอบอีกครั้ง?";
				close;
			}
		}
		else {
			mes "ห้ะ? อะไรนำเจ้ามาที่นี่ นี่มันเป็นที่ที่อันตรายมากสำหรับ Novice อย่างเจ้า!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Greetings.";
			next;
			mes "^0066FF[ Father Rubalkabara ]^000000";
			mes "ยินดีต้อนรับ ทำใจให้สบายและนั่งลงรับสารจากพระเจ้าจากข้า สถานที่แห่งนี้สวยงาม แม้นว่าอันตรายอาจมาพร้อมกับความเงียบสงบ..";
			close;
		}
		else {
			mes "Oh ho...";
			mes "เจ้ามาถึงนี่เพื่อการฝึกงั้นหรือ? หรือเจ้าเป็นคนไร้จุดหมาย?";
			next;
			mes "^0066FF[ Father Rubalkabara ]^000000";
			mes "ไม่ว่าเจ้าเป็นใคร โปรดระวังตัวเจ้าเอง มอนสเตอร์ที่นี่แข็งแกร่งอย่างน่าพิศวง ขัดกับลักษณะที่น่ารักของพวกมัน";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Ascetic#2aco	95,{
	mes "^0066FF[ Mother Mathilda ]^000000";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "ฉันจะส่งพิราบสื่อสารส่งข่าวไปยัง Prontera เมื่อเจ้ากลับไป, เขาจะได้รับข้อความจากข้าแล้ว";
			next;
			mes "^0066FF[ Mother Mathilda ]^000000";
			mes "ฉันจะสวดขอพรจากพระเจ้า และหวังให้เจ้าได้เป็น Acolyte เร็วๆนี้";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "อา..เจ้าจะต้องเป็นหนึ่งในผู้สมัครเป็น Acolyte ยินดีต้อนรับด้วยความจริงใจ";
				next;
				mes "^0066FF[ Mother Mathilda ]^000000";
				mes "เจ้าชื่ออะไร..? " + strcharinfo(0) + "? ไหนดู... เจ้าอยู่รายชื่อของฉัน";
				next;
				mes "^0066FF[ Mother Mathilda ]^000000";
				mes "ฉันจะส่งข้อความไปเพื่อยืนยันว่า " + strcharinfo(0) + " ได้มาพบฉันแล้ว";
				next;
				mes "^0066FF[ Mother Mathilda ]^000000";
				mes "จงกลับไปยัง Prontera เถิด";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "Ah...!";
				mes "...เจ้าจะต้องเป็น";
				mes "หนึ่งในผู้สมัครเป็น Acolyte";
				mes "ยินดีต้อนรับด้วยความจริงใจ";
				next;
				mes "^0066FF[ Mother Mathilda ]^000000";
				mes "เอาล่ะ เจ้าชื่ออะไรนะ?";
				mes "" + strcharinfo(0) + "? ไหนดูซิ...";
				next;
				mes "^0066FF[ Mother Mathilda ]^000000";
				mes "หืม...";
				mes "ดูเหมือนว่า";
				mes "ไม่มีรายชื่อของเจ้านะ...";
				next;
				mes "^0066FF[ Mother Mathilda ]^000000";
				mes "บางทีเจ้าน่าจะกลับไปที่ Prontera และตรวจสอบใหม่อีกครั้งนะ";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "สวัสดี~";
			next;
			mes "^0066FF[ Mother Mathilda ]^000000";
			mes "เจ้าฝึกมานานแค่ไหนแล้ว หวังว่าเจ้าจะเพลิดเพลินกับการอยู่กับพระเจ้า";
			close;
		}
		else {
			mes "พระองค์ อยู่ข้างเจ้า...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Ascetic#3aco	98,{
	mes "^0066FF[ Father Yosuke ]^000000";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "อะไรนะ?";
			next;
			mes "^0066FF[ Father Yosuke ]^000000";
			mes "เจ้ามีธุระอะไรกับข้างั้นหรือ?ยังไม่กลับไปอีก!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "เฮ้";
				mes "เหมือนว่าเจ้า";
				mes "จะเป็นผู้ที่สมัครเป็น";
				mes "Acolyte";
				mes "ใช่ไหม?";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "ไม่เลว ไม่เลว เจ้าพยายามสารภาพบาปได้ดี";
				mes "เจ้าชื่ออะไรนะ?";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "" + strcharinfo(0) + ", ห้ะ?";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "โอเค... ข้าจะแจ้งไปยังโบสถ์ ว่า " + strcharinfo(0) + ", มาพบข้าแล้ว";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "ตอนนี้เจ้ากลับไปได้แล้วและจงเป็น Acolyte เถิด เจ้าเด็กน้อย";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "Hey.";
				mes "เจ้าดูเหมือนผู้ที่สมัครเป็น Acolyte ข้าคิดถูกไหม?";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "ไม่เลวเท่าไร เจ้ามาจาก Prontera เจ้าชื่ออะไรนะ เด็กน้อย?";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "" + strcharinfo(0) + ", ทำไมไม่มีชื่อของเจ้าในรายชื่อของข้า?";
				next;
				mes "^0066FF[ Father Yosuke ]^000000";
				mes "เจ้าพลาดแล้วล่ะ จงกลับไปที่โบสถ์แล้วตรวจสอบอีกครั้ง";
				close;
			}
		}
		else {
			mes "เจ้า...";
			mes "Novice.";
			mes "มีอะไร";
			mes "ที่ต้องการบอกข้า?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "เฮ้...";
			next;
			mes "^0066FF[ Father Yosuke ]^000000";
			mes "ถ้าเจ้าชอบ มานั่งสมาธิที่นี่กับข้า พระเจ้าคือแรงบันดาลใจของเรา...";
			close;
		}
		else {
			mes "เจ้ามีอะไรจะพูดไหม? เพราะมันโชคไม่ดีเลย...,";
			mes "ข้าไม่ได้รับการตอบกลับมา.....";
			close;
		}
	}
}

