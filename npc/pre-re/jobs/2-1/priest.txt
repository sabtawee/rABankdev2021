//*********************************************************************************************************************************************************************************\\
//============================================ Father Thomas: Job changer, Test 1 =============================================\\
//*********************************************************************************************************************************************************************************\\
prt_church.gat,16,41,4	script	Father Thomas	60,{

	if(JobLevel == 50) {
	if (Class!=4) goto Notache;
	if(SkillPoint >0) goto L_notUseAll;

	 mes "[ Jobchanger ]";
	 mes "ท่านต้องการเปลื่ยนอาชีพเป็นpriest ใช่หรือไม่";
	 next;
	 menu "ใช่",-,"ไม่ใช่",L_close;
	 mes "[ Jobchanger ]";
	 mes "ถ้าเป็นอย่างนั้น ข้าก็จะเปลื่ยนให้เจ้าทันที";
	 next;
	 JobChange 8;
	set zeny,zeny+50000;
	 mes "เจ้าได้เป็น priestเรียบร้อยแล้ว";
	 close;

	L_close:
	 mes "[ Jobchanger ]";
	 mes "เราคงมีโอกาศได้พบกันใหม่อีกแน่ บาย";
	 close;

	L_notUseAll:
	 mes "[ Jobchanger ]";
	 mes "ท่านยังใช้ Skill Point ไม่หมด";
	 close;

	Notache:
	 mes "[ Jobchanger ]";
	 mes "คุณไม่ได้ นักบุญนิ";
	 close;


	}
	mes "[Father Thomas]";
	if (class == Job_Novice) goto L_Novice;
	if (class == Job_Priest) goto L_Priest;
	if (class == Job_Acolyte) goto L_Start;

L_Other:
	mes "พระเจ้าจะอยู่กับท่าน";
	next;
	mes "[Father Thomas]";
	mes "คริสตจักรเป็นสถานที่ที่ปลอดภัยที่สุดในโลกในเวลานี้";
	close;

L_Novice:
	mes "เจ้าเด็กน้อย เจ้าเดินทางถึงโบถของ Prontera เพื่ออะไรกัน?";
	next;
	menu "ข้าต้องการเป้น Acolyte.",M_0a, "ข้าต้องการเป็น ^5533FFPriest^000000.",M_0b, "ข้าแค่เดินดูรอบๆ",M_0End;

	M_0a:
		mes "[Father Thomas]";
		mes "ฮ่า ฮ่า.. หากท่านต้องการจะเป็น Acolyte ? ท่านกรุณาไปลงชื่อที่ห้องตรงข้ามกับที่พักของข้า";
		close;
	M_0b:
		mes "[Father Thomas]";
		mes "ฮ่า ฮ่า...หากท่านอยากจะเป็น priest ท่านต้องเป็น Acolyte มาก่อนแล้วไปยังห้องพักตรงข้ามกับข้า";
		close;
	M_0End:
		mes "[Father Thomas]";
		mes "ท่านคงจะมาที่นี่เพื่อหลบหนีความวุ่นวายของโลกภายนอกล่ะสิ?";
		close;

L_Priest:
	mes "นี่จะเป็นคำอวรพรแก่พระผู้เป็นเจ้า ที่กรุณาฟังคำสั่งสอนของข้ามาเสมอ";
	next;
	mes "[Father Thomas]";
	mes "ข้าไม่รู้ว่าบุตรแห่งพระผู้เป็นเจ้าจะเดินไปทางแห่งแสงหรือเดินไปยังทางด้านมืด แต่ข้าอธิฐานมันเป็นทางที่ดีที่สุดแล้วกัน";
	next;
	mes "[Father Thomas]";
	mes "แล้วเจ้ามาทำอะไรที่นี่วันนี้ล่ะ?";
	next;
	menu "ข้าแค่อยากเดินมาดู",M_1a, "ข้ามาเพื่ออยากช่วย Acolytes.",M_1b,"ชีวิตมันมันช่างลำบากนัก",M_1End;

	M_1a:
		mes "[Father Thomas]";
		mes "โอ้ ข้าดีใจที่ท่านมาถึงที่นี่ ท่านคงจะมารับคำสั่งสอน และปฏิบัติตามคำสั่งของพระผู้เป็นเจ้าสินะ";
		close;
		
	M_1b:
		mes "[Father Thomas]";
		mes "โอ้ ข้าต้องขอบคุณท่าน ที่ท่านจะพยามจะกลับมาช่วย Acolytes";
		emotion 5;
		next;
		mes "[Father Thomas]";
		mes "การฝึกฝนเพื่อประสบการณ์คือสิ่งสำคัญที่พวกเขา จะได้รับ.. ! ";
		next;
		mes "[Father Thomas]";
		mes "หากท่านต้องการจะช่วยท่านจะต้องมี ^5533FFROSARY^000000.";
		mes "และมีการทดสอบสำหรับ Acolyte อยู่ 3 อย่าง แต่มีเพียง 2 อย่างเท่านั้นที่เข้าจะขอความช่วยเหลือจากท่านได้";
		next;
		mes "[Father Thomas]";
		mes "ท่านคงพร้อมแล้วใช่มั้ย?";
		next;
		menu "ใช่ ข้าพร้อมแล้ว",sM_1a, "แล้วข้าจะมาใหม่",sM_1End;

		sM_1a:
			if (countitem(2608) < 1) goto sL_NoRosary;
			mes "[Father Thomas]";
			mes "ดีมาก.. ข้าจะพาเจ้าไปหา Acolyte อีกสักครู่หนึ่ง";
			next;
			mes "[Father Thomas]";
			mes "ข้าหวังว่าเจ้าจะใช้โอกาสนี้คอยนำทางพวกเขาให้เขากลายเป็น Priest ที่ดี";
			next;
			if((getareausers("job_prist.gat", 8, 34, 39,109)) == 1) warp "job_prist.gat", 24, 44;
			if((getareausers("job_prist.gat", 160, 14, 175, 178)) == 1) warp "job_prist.gat", 98, 40;
			if((getareausers("job_prist.gat", 90, 34, 105,105)) == 1) warp "job_prist.gat", 168, 17;
			mes "[Father Thomas]";
			mes "อืม........ รอเดี๋ยว..... ข้าขอตรวจดูรายชื่อก่อน.... ตอนนี้ยังไม่มีการทดสอบที่เหมาะสมกับท่าน";
			mes "ข้าว่าท่านควรมาใหม่ในภายหลัง";
			close;

			sL_NoRosary:
				mes "[Father Thomas]";
				mes "อืม ข้าไม่เห็น ^5533FFROSARY^000000 ในตัวของท่านเลย";
				next;
				mes "[Father Thomas]";
				mes "หากท่านต้องการจะช่วยเหลือ Acolytes ท่านจำเป็นต้องมี ^5533FFROSARY^000000.";
				mes "แล้วค่อยกลับมาหาข้าใหม่อีกครั้ง";
				close;
		sM_1End:
			mes "[Father Thomas]";
			mes "โอ้ ไม่เป็นไร แต่จงอย่าลืม ท่านต้องมี ^5533FFFROSARY^000000 แล้วค่อยกลับมาหา";
			close;

	M_1End:
		mes "[Father Thomas]";
		mes "พระเจ้าจะคอยช่วยเหลือและคอยบรรเทาความทุกข์แก่ผู้ที่ รู้สึกเป็นทุกข์ โดยใช้พลังอำนาจในการรักษาร่างกายนี้";
		percentheal 100,100;
		next;
		mes "[Father Thomas]";
		mes "ท่านจะรู้สึกดีขึ้น หากท่านเดินอยู่ในเส้นทางที่ดี พระเจ้าคงจะอวยพรท่าน";
		close;


L_Start:
	if (PRIEST_Q == 1) goto L_Test1;
	if (PRIEST_Q == 2) goto L_Test2;
	if (PRIEST_Q == 3) goto L_Test3;
	if (PRIEST_Q == 4) goto L_Change;
	mes "ท่านจะได้รับความคุ้มครองจากสวรรค์พี่น้อง วัตถุประสงค์ของท่านในการมาที่นี่คือ?";
	next;
	menu "ข้ามาที่นี่เพื่อเป็น priest.",M_2a, "ข้ามาพบท่าน",M_2b, "ข้าอยากรู้ว่าที่นี่มีอะไร",M_2End;

	M_2a:
		mes "[Father Thomas]";
		mes "ถ้าท่านต้องการเป็น Priest งั้นท่านต้องได้รับการทดสอบ แต่ก่อนการทดสอบ ท่านต้องเป็น Acolyte job level 40 แล้ว..";
		next;
		mes "[Father Thomas]";
		mes "ก่อนอื่นท่านต้องลงทะเบียนเสียก่อน.. ลงละเบียนเลยละกัน";
		next;
		mes "[Father Thomas]";
		if (JobLevel < 40) goto sL_LowLvl;
		if (JobLevel == 50) goto sL_HighLvl;
		mes "ดีมากท่าน " + strcharinfo(0) + " ข้าจะบอกเกี่ยวกับงานที่ท่านต้องทำก่อนที่ท่านจะกลายมาเปHน Priest";
		next;
		mes "[Father Thomas]";
		mes "'งานแรกที่ท่านต้องทำ ท่านต้องไปพูดคุยกับ  Clerics คนที่ฉลาดมาก เขาจะอยู่ในส่วนต่างๆ ของ Rune Midgard.";
		next;
		mes "[Father Thomas]";
		mes "การทดสอบที่ 2 จะประกอบไปด้วย 3 ย่อย ที่จะต้องไปเผชิญหน้ากับมอนสเตอร์ undead.";
		mes "การทดสอบสุดท้ายท่าจะต้องหลีกเลี่ยงความเสียหาย และคิดหาทางออกโดยไม่เสียเลือดเนื้อ";
		next;
		mes "[Father Thomas]";
		mes "หลังจากที่เสร็จสิ้นการทดสอบทั้ง 2 แล้ว ท่านก็จะสามารถกลายเป็นพระได้ทันที";
		next;
		mes "[Father Thomas]";
		set JBLVL, 40;
		goto L_Test1;

		sL_LowLvl:
			mes "ข้าเสียใจ ข้าคงให้ท่านเป็น Pirest ไม่ได้หรอก ท่านมี level Job ไม่ถึง 40";
			close;

		sL_HighLvl:
			mes "ผู้ที่บรรลุ job level 50 แล้วจะไม่ต้องไปแสวงบุญ";
			mes "เนื่องจากเขาได้พิสูจน์แล้วว่าเขาได้ทำสิ่งต่างๆมาพอแล้ว";
			emotion 0;
			next;
			mes "[Father Thomas]";
			set PRIEST_Q, 2;
			set JBLVL, 50;
			goto L_Test2;
	M_2b:
		mes "[Father Thomas]";
		mes "Priests เป็นผู้เดินตามทางแห่งพระผู้เป็นเจ้า การจะก้าวเข้าสู้ขั้นนี้ได้ท่านต้องมีอาชีพเป็น Acolytes.";
		next;
		mes "[Father Thomas]";
		mes "และท่านต้องมีเลเวลจ๊อบอย่างน้อยที่ 40 จากนั้นท่านจะต้องทำการทดสอบเพื่อพิสูจตัวเอง";
		next;
		mes "[Father Thomas]";
		mes "หากท่านกลายเป็น Priest ท่านจะเป็นอาชีพที่มีความสำคัญมากสำคัญมากกับอาชีพอื่นๆ";
		close;
	M_2End:
		mes "[Father Thomas]";
		mes "โอ้.. นี่คือที่ที่เราจะมาสงบอารมณ์ได้";
		mes "ท่านจงอย่าลืมหน้าที่ของ Acolyte ที่ข้าคอยสอนเจ้าล่ะ";
		close;



L_Test1:
	if (PRIEST_Q2 == 1) goto L_NotDone1a;
	if (PRIEST_Q2 == 2) goto L_NotDone1b;
	if (PRIEST_Q2 == 3) goto L_NotDone1c;
	if (PRIEST_Q2 == 4) goto L_Done1;
	mes "ก่อนอื่น ท่านต้องไปทำการแสวงบุญ";
	mes "ท่านต้องไปพบ Priest ผู้สันโดษ 3 ท่าน";
	next;
	mes "[Father Thomas]";
	mes "คนแรก ^5533FFบาทหลวง Rubalkubara^000000 ท่านจะต้องขึ้นเหนือไปทาง Protera 1 แผนที่ และไปทางทิศตะวันออก 2 แผนที่ อยู่ใกล้ๆ กับ St. Capitolina Abbey";
	next;
	mes "[Father Thomas]";
	mes "คนที่สอง ^5533FFแม่ชี Matilda^000000 เธอจะอยู่บริเวณเหนือแผนที่ Morroc 1 แผนที่";
	next;
	mes "[Father Thomas]";
	mes "คนสุดท้าย ^5533FFFather Yosuke^000000";
	mes "ข้าได้ยินมาว่าเขาจะอยู่แถวบริเวณเกาะเล็กๆ 2 เกาะทางตอนเหนือของ Prontera";
	next;
	mes "[Father Thomas]";
	mes "ขอให้ท่านปลอดภัยในการเดินทาง หากมีปัญหาใดๆ ให้กลับมาพูดคุยกับข้าได้ทุกเมื่อ";
	mes "จงอย่าลืม เมื่อทำงานเสร็จแล้ว ให้ท่านกลับมาหาข้า";
	next;
	mes "[Father Thomas]";
	mes "พระเจ้าจะอยู่ท่าน...";
	set PRIEST_Q, 1;
	set PRIEST_Q2, 1;
	close;

	L_NotDone1a:
		mes "อะไร? ทำไมท่านยังอยู่ที่นี่? หรือว่าท่านลืมงานชิ้นแรกแล้วงั้นเหรอ?";
		next;
		menu "ใช่ โปรดบอกข้าอีกครั้งเกี่ยวกับงานชิ้นแรก?",sM_3a, "ไม่ ข้าไม่ได้ลืม",sM_3End;

		sM_3a:
			mes "[Father Thomas]";
			mes "ตามหา ^5533FFบาทหลวง Rubalkubara^000000 ท่านจะต้องขึ้นเหนือไปทาง Protera 1 แผนที่ และไปทางทิศตะวันออก 2 แผนที่ อยู่ใกล้ๆ กับ St. Capitolina Abbey";
			close;

		sM_3End:
			mes "[Father Thomas]";
			mes "โอ้ หากท่านไม่มีคำถามอะไร ข้าว่าท่านควรจะไปพูดคัยกับ Sister Cecila ที่อยู่ใกล้ๆ กับทางเข้า";
			next;
			mes "[Father Thomas]";
			mes "โปรดรีบๆ เดินทางต่อไป พระเจ้าได้มองมาที่ท่านด้วยรอยยิ้ม...";
			close;
	L_NotDone1b:
		mes "หืม??";
		emotion 1;
		next;
		mes "[Father Thomas]";
		mes "ตามหา ^5533FFแม่ชี Matilda^000000 เธอจะอยู่บริเวณเหนือแผนที่ Morroc 1 แผนที่";
		close;
		
	L_NotDone1c:
		mes "ตามหา ^5533FFFather Yosuke^000000";
		mes "ข้าได้ยินมาว่าเขาจะอยู่แถวบริเวณเกาะเล็กๆ 2 เกาะทางตอนเหนือของ Prontera";
		close;
		
	L_Done1:
		mes "ท่านควรไปพูดคุยกับ Clerics? เขาจะมอบงานใหม่ให้ท่าน หากท่านสำเร็จงานแรกแล้ว";
		next;
		mes "[Father Thomas]";
		mes "ตอนนี้ท่านควรจะเริ่มงานถัดไปได้ เตรียมตัวเผชิญหน้ากับความชัวทั้งเก้า ท่านสามารถให้ Priest ช่วยท่านสำหรับการทดสอบนี้ได้";
		set PRIEST_Q, 2;
		set PRIEST_Q2, 0;
		next;
		mes "[Father Thomas]";

L_Test2:
	if(PRIEST_Q2 == 1) goto L_ReTest2;
	if(PRIEST_Q2 == 2) goto L_Done2;
	mes "แล้ว ท่านจะเริ่มผ่านความชั่วทั้งเก้าเลยไหม?";
	M_Menu:
	next;
	menu "แน่นอน",M_4a, "เอ่อ! คราวหน้าแล้วกัน",M_4End;
                                             	            
	M_4a:
		mes "[Father Thomas]";
		mes "ดีมาก ข้าจะส่งท่านไปที่ ^5544FFบาทหลวง Peter^000000 เขาจะบอกเกี่ยวกับบทสอบที่ 2";
		next;
		set PRIEST_Q2, 1;
		warp "job_prist.gat", 24, 180;
		savepoint "prt_church.gat", 16, 37;
		end;
		
	M_4End:
		mes "[Father Thomas]";
		mes "โอเค แล้วรีบกลับมาล่ะ ข้าหวังว่าท่านจะสามารถผ่านมันไปได้โดยไม่มีปัญหา";
		close;

	L_ReTest2:
		mes "ท่านดูลำบากจริงๆ ข้าว่าท่านคงจะลำบากมากขึ้นหากท่านได้เป็น Priest.";
		mes "ขอให้ท่านจงอย่ายอมแพ้ ข้าจะให้ท่านได้เริ่มทดสอบใหม่อีกครั้ง?";
		goto M_Menu;
		
	L_Done2:
		mes "ท่านได้สำเสร็จสิ้นการทดสอบแล้ว";
		mes "ท่านสามารถผ่านความยากลำบากที่พอจะเป็น Priest ได้แล้ว";
		next;
		mes "[Father Thomas]";
		mes "ตอนนี้ให้ไปหา ^5533FFแม่ชี Cecile^000000 และสาบานตนกับเธอ";
		mes "แค่ดำเนินตามคำตอบของพระเจ้าเมื่อท่านพบคำถามใดๆ";
		next;
		mes "[Father Thomas]";
		mes "ข้าจะรอท่านอยู่ที่นี่ ขอให้โชคดี เด็กน้อย !!";
		set PRIEST_Q, 3;
		set PRIEST_Q2, 0;
		close;

L_Test3:
	mes "พูดคุยกับ ^5533FFแม่ชี Cecile^000000 เพื่อจะผ่านการทดสอบสุดท้าย";
	next;
	mes "[Father Thomas]";
	mes "โชคดี ข้าจะรอฟังข่าวดีของท่านอยู่ที่นี่";
	close;


L_Change:
	if (SkillPoint > 0) mes "เดี๋ยว สกิลพ้อยท์ของท่านยังใช้ไม่ครบ";
       	if (SkillPoint > 0) close;
	mes "ยินดีด้วย ตอนนี้ท่านผ่านการทดสอบทั้งหมดเสร็จสิ้นลงแล้ว ข้าสามารถทำให้ท่านได้เป็น Pirest";
	emotion 21;
	next;
	mes "[Father Thomas]";
	mes " โอ้ พระเจ้าโปรดมอบพลังให้แก่ผู้ที่อยู่ ณ เบื้องหน้าท่าน";
	mes " มอบอำนาขและความศรัทธาแด่เขาเพื่อเผยแพร่คำสอนของท่าน";
	next;
	jobchange Job_Priest;
	mes "[Father Thomas]";
	mes " นี่เป็นจุดจบของการทดสอบแล้ว";
	mes " ขอบคุณท่านมาก ท่านสามารถไปได้แล้ว";
	next;
	if (JBLVL == 40) getitem 1550, 1;
	if (JBLVL == 50) getitem 1551, 1;
	callfunc "F_ClearJobVar";		// clears all job variables for the current player
	mes "[Father Thomas]";
	mes " ข้าจะขอมอบหนังสือเล่มนี้เป็นรางวัลสำหรับการสำเร็จการทดสอบ หนังสือเล่มนี้จะช่วยท่านเพิ่มความเข้าใจเกี่ยวกับพระเจ้า";
	next;
	mes "[Father Thomas]";
	mes "ข้าหวังว่าท่านสามารถเป็นแบบอย่างที่ดีแก่ Acolytes หนุ่ม สาว ทั้วไปใน Rune Midgard.";
	mes "ช่วยเป็น Priest ที่ดีและนำสันติภาพสู่โลกนี้!";
	close;
}


//----------------------------------------------------------------------------------------------------------------------------------------------------------\\
//					Test 1					              \\
//********************************************************************************************************************************\\
// Function: Father Rubalkabara --------------------------------------------------------------------------
function	script	F_FatherRub	{
	if(PRIEST_Q == 1 && PRIEST_Q2==1) goto L_Test;
	if(PRIEST_Q==1 && PRIEST_Q2 > 1) goto L_Done;
	mes " เป็นเรื่องที่น่ายินดี ที่ข้าได้เห็นท่านอีกครั้ง ข้าหวังว่าท่านจะเดินไปตามเส้นทางที่ดี โดยการช่วยเหลือผู้อื่น";
	close;

L_Test:
	mes "โอ้.. สวัสดี";
	next;
	mes "[Father Rubalkabara]";
	mes "ที่ท่านกำลังมาที่นี่เพราะท่านต้องการจะรับใช้เพราะเจ้าด้วยความสามารถที่ยิ่งใหญ่กว่าใช่มั้ย?";
	emotion 1;
	next;
	menu "ใช่ ข้าเชื่อว่านี่คือเส้นทางที่เหมาะสม",-, "ไม่ ข้าแค่แวะมาทักทาย",M_No;

		mes "[Father Rubalkabara]";
		mes "ฮ่า ฮ่า! ยอดเยี่ยม  พวกเรากำลังต้องการคนเพิ่มขึ้นเพื่อช่วยเหลือโลกที่เต็มไปด้วยความชั่วร้ายนี้!";
		emotion 21;
		next;
		mes "[Father Rubalkabara]";
		mes "ข้าได้เห็นหัวใจอันบริสุทธิ์ของท่าน ท่านจงไปรับพรจาก ^5533FFแม่ชี Marthilda^000000 ในงานถัดไป";
		next;
		mes "[Father Rubalkabara]";
		mes "ท่านสามารถพบเธอได้ทางตอนเหนือของ Morroc ขอให้เดินทางปลอดภัยและโชคดีกับการเป็น Priest";
		set PRIEST_Q2, 2;
		close;
	M_No:
		mes "[Father Rubalkabara]";
		mes "โอ้ ข้าต้องสวัสดีท่านเช่นกัน..!!";
		emotion 4;
		close;

L_Done:
	mes "ตามหา ^5533FFแม่ชี Marthilda^000000 ขอให้เดินทางปลอดภัยและโชคดีกับการเป็น Priest";
	close;
}

// Function: Mother Marthilda  --------------------------------------------------------------------------
function	script	F_MotherMart	{

	if(PRIEST_Q==1 && PRIEST_Q2==2) goto L_Test;
	if(PRIEST_Q==1 && PRIEST_Q2 > 2) goto L_Done;
	mes "โอ้ว.. ข้าได้เห็นการเติบโตของท่านด้วยความน่ายินดี มันจะทำให้ผู้รับใช้พระเจ้าอย่างข้าได้ปรับปรุงตัวอย่างต่อเนื่อง";
	mes "ข้าอยากให้ท่านได้จำไว้ว่า ท่านจะประสบความสำเร็จเสมอหากท่านมีความเชื่อ";
	close;

L_Test:
	mes "อ้า.. ท่านมาที่นี่เพื่อการทดสอบ Priest?  ข้าคงไม่ต้องบอกท่านหรอกนะว่าท่านทำงานได้ดีมากจนมาถึงขั้นนี้ได้";
	next;
	mes "[Mother Marthilda]";
	mes "ที่แสดงให้เห็นว่าท่านมีความเชื่อมั่น ทำให้ทางเดินทางผ่านการทดสอบมายาวไกล";
	next;
	mes "[Mother Marthilda]";
	mes "ตามหา ^5533FFบาทหลวง Yosuke^000000 ข้าได้ยินมาว่าเขาจะอยู่แถวบริเวณเกาะเล็กๆ 2 เกาะทางตอนเหนือของ Prontera";
	set PRIEST_Q2, 3;
	close;

L_Done:
	mes "ท่านผ่านการทดสอบ ตามหา ^5533FFบาทหลวง Yosuke^000000 ขอให้โชคดี";
	close;
}

// Function: Father Yosuke --------------------------------------------------------------------------
function	script	F_FatherYos	{

	if(PRIEST_Q==1 && PRIEST_Q2==3) goto L_Test;
	if(PRIEST_Q==1 && PRIEST_Q2==4) goto L_Done;
	mes "เพียงเพราะท่านกำลังเป็นผู้รับใช้ของพระเจ้าไม่ได้หมายความว่าท่านสามารถมาที่นี่ได้..";
	close;

L_Test:
	mes "หืม...? ท่านต้องการให้ข้าทดสอบเพื่อท่านจะกลายเป็น Priest? ท่านคงแน่ใจที่จะทำสิ่งนั้นแล้วนะ?";
	emotion 1;
	next;
	mes "[Father Yosuke]";
	mes "ดีมาก  ข้าจะให้ท่านผ่าน  กลับไปพูดคุยกับหลวงพ่อ Thomas ทีเหลือจะเป็นหน้าที่ของเขา";
	set PRIEST_Q2, 4;
	close;

L_Done:
	mes "หือ? ท่านยังมัวทำอะไรอยู่ที่...? ข้าบอกท่านให้กลับไปที่โบสถ์.... ข้ากำลังยุ่ง....";
	emotion 1;
	close;
}


//**********************************************************************************************************************************************************************************\\
//================================================== Father Peter: Test 2 ====================================================\\
//**********************************************************************************************************************************************************************************\\
job_prist.gat,24,186,4	script	Father Peter	110,{

L_Start:
	mes "[Father Peter]";
	mes " พรเจ้าอวยพร!! ท่านกลับมาแล้ว!";
	mes " ยินดีด้วยที่ท่านสามารถผ่านการทดสอบระดับแรกไปได้ด้วยดี";
	mes " ข้ามีชื่อ Peter";
	next;
	mes "[Father Peter]";
	mes " ยังไงก็ตาม เมื่อท่านมาแล้ว ข้าจะบอกท่านเกี่ยวกับความชั่วทั้งเก้า?";
	mes " ท่านทราบไหมว่ามันคืออะไร?";
	next;
	menu "ใช่ข้า ข้ารู้",-, "ไม่ ข้าไม่รู้..?",M_1b;

		mes "[Father Peter]";
		mes " งั้นจะเริ่มเลยไหม";
		mes " ง่านมาก แค่กำจัดพวกมันให้หมด!";
		goto L_Cont;
	M_1b:
		mes "[Father Peter]";
		mes " โอ้ ข้าว่าข้าควรจะอธิบายให้ท่านฟัง";
		mes " หลังจากที่ท่านได้ฟังที่สิ่งที่ข้าพูดจนจบ ข้าว่าท่านคงจะเข้าใจ";
		next;

	L_Cont:
	mes "[Father Peter]";
	mes " อะไรที่เอาชนะความชั่วร้าย? และทำลายมัน";
	mes " แล้วอะไรคือสิ่งชัวร้าย? ผี ปีศาจ หรืออื่นๆ...";
	next;
	mes "[Father Peter]";
	mes " มีความชั่วร้ายอยู่มากมายในโลกใบนี้";
	mes " ความชั่วร้ายมักจะหยอกล้อกับพระผู้เป็นเจ้า";
	next;
	mes "[Father Peter]";
	mes " priests อย่างพวกเรา ได้รับหน้าที่ให้กำจัดความชั่วร้ายทั้งหมด!";
	mes " นั่นคือวิธีเดียวที่เราสามารถนำความสุขมาสู่โลกใบนี้!";
	next;
	mes "[Father Peter]";
	mes " หากท่านลองทุ่มเทความสามารถทั้งหมด ท่านก็ยังไม่อาจจะสามารถเอาชนะความท้าทายเหล่านี้ได้";
	mes " ดังนั้นข้าคิดว่ามันจำเป็นที่จะให้ Priest บางคนช่วยท่านให้ท่านผ่านการทดสอบนี้ไป";
	next;
	mes "[Father Peter]";
	mes " ดี ท่านจะเริ่มเลยมั้ย?";
	M_Menu:
	next;
	menu " ตกลง",M_2a, " รอสักเดี๋ยวก่อน",M_2b, "ข้าต้องการกลับเมือง",M_2c;

	M_2a:
		mes "[Father Peter]";
		mes " โอเค หากพร้อมก็เข้าไปที่ waiting room.";
		close;

	M_2b:
		mes "[Father Peter]";
		mes " โอ้ นี่ท่านยังต้องการความพร้อมอีกเหรอ?";
		mes " แต่ไม่มีปัญหาหากต้องการจะเริ่มบทดทอบ ท่านก็กลับมาหาข้าใหม่แล้วกัน";
		close;
	M_2c:
		mes "[Father Peter]";
		mes " อะไร? ทั้งต้องการจะกลับไป? ทั้งที่ท่านมาถึงนี่แล้วงั้นเหรอ?";
		mes ".....";
		next;
		mes "[Father Peter]";
		mes " มันอาจจะเป็นครั้งแรกสำหรับท่าน จึงทำให้ท่านรู้สึกกลัว";
		mes " ท่านสามารถกลับมาได้ทุกเมื่อ หากท่านพร้อม";
		next;
		warp "prt_church.gat", 16, 37;
		close;

OnInit:
	waitingroom "Priest Test Waiting Room",8,"Father Peter::OnStart",1;
	end;
OnStart:
	set $@PrstUsers, getareausers("job_prist.gat", 8, 34, 39,109);
	set $@PrstUsers, $@PrstUsers + getareausers("job_prist.gat", 160, 14, 175, 178);
	set $@PrstUsers, $@PrstUsers + getareausers("job_prist.gat", 90, 34, 105,105);
	if($@PrstUsers > 0) end;				// stops the rest of the script from running if there is already another player taking the test

	if((getwaitingroomstate(33)) == 0) end;			// stops the rest of the script from running if there is no one in the waiting room
	disablenpc "prst1_1";	//disables the exit warp
	disablenpc "prst2_1";
	warpwaitingpc "job_prist.gat", 24, 44;
	killmonsterall "job_prist.gat";
	donpcevent "PrstTest2_1::OnStart";
	end;
}


//----------------------------------------------------------------------------------------------------------------------------------------------------------\\
//				Test 2, Part 1: Kill the Zombies				              \\
//********************************************************************************************************************************\\
job_prist.gat,1,1,1	script	PrstTest2_1	-1,{
OnStart:
	set $@PrstRm, 1;		//used to determine what areawarp and areaannounce to use
	enablenpc "Zombie1_Trig";
	enablenpc "Zombie2_Trig";
	enablenpc "Zombie3_Trig";
	enablenpc "Zombie4_Trig";
	enablenpc "Zombie5_Trig";
	set $@mob, 13;
	initnpctimer;
	end;

OnMobDead:
	set $@mob, $@mob - 1;
	if($@mob > 0) end;

	enablenpc "prst1_1";
	areaannounce "job_prist.gat",8,34,39,109,"[Father Peter]: ทำได้ดีมากลูกข้า ข้าจะพาท่านไปยังบททดสอบถัดไป",8;
	initnpctimer "prst1_1";		//starts a 30 sec timer.  this will warp the player even if they don't enter the warp to keep the test going
	end;

OnTimer500:
	areaannounce "job_prist.gat",8,34,39,109,"[Father Peter]: เพื่อผ่านการทดสอบนี้ท่านจะต้องกำจัด zomibes ทั้งหมดที่ปรากฏ",8;
	end;
OnTimer2500:
	stopnpctimer;
	initnpctimer "TimerPrst";		// starts the 5 min test timer
	areaannounce "job_prist.gat",8,34,39,109,"[Father Peter]: ท่านใช้เวลามากเกินไป",8;
	end;
}

// These hidden warps spawn the monsters when you walk
// First set  of Zombies------------------------------------------------------------
job_prist.gat,23,52,1	script	Zombie1_Trig	139,16,0,{

	monster "job_prist.gat",24,52,"Robbery",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",18,52,"Evilness",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",30,52,"Jealousy",1015,1,"PrstTest2_1::OnMobDead";
	disablenpc "Zombie1_Trig";		//disables the npc so that it can't spawn the monsters more than once
	end;
}

// Second set  of Zombies----------------------------------------------------
job_prist.gat,23,62,1	script	Zombie2_Trig	139,16,0,{

	monster "job_prist.gat",21,62,"Anger",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",27,62,"Wonderness",1015,1,"PrstTest2_1::OnMobDead";
	disablenpc "Zombie2_Trig";
	end;
}

// Third set  of Zombies--------------------------------------------------------
job_prist.gat,23,72,1	script	Zombie3_Trig	139,16,0,{

	monster "job_prist.gat",24,72,"Cockyness",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",18,72,"Slutty",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",30,72,"Lazyness",1015,1,"PrstTest2_1::OnMobDead";
	disablenpc "Zombie3_Trig";
	end;
}

// Fourth set  of Zombies--------------------------------------------------------
job_prist.gat,23,82,1	script	Zombie4_Trig	139,16,0,{

	monster "job_prist.gat",21,82,"Greed",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",27,82,"Greedyness",1015,1,"PrstTest2_1::OnMobDead";
	disablenpc "Zombie4_Trig";
	end;
}

// Fifth set of Zombies ---------------------------------------------------------
job_prist.gat,23,92,1	script	Zombie5_Trig	139,16,0,{

	monster "job_prist.gat",24,92,"Faithless",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",18,92,"Non-believer",1015,1,"PrstTest2_1::OnMobDead";
	monster "job_prist.gat",30,92,"Scaryness",1015,1,"PrstTest2_1::OnMobDead";
	disablenpc "Zombie5_Trig";
	end;
}


// End warp for 1st part of test--------------------------------------------------------------------
job_prist.gat,24,109,1	script	prst1_1	45,3,3,{

	if(class == Priest) end;		//If a Priest friend steps on the warp nothing happens.  Need the aco to step on the warp.
OnTimer30000:
	stopnpctimer;
	areawarp "job_prist.gat",8,34,39,109,"job_prist.gat",168,17;
	killmonsterall "job_prist.gat";
	donpcevent "PrstTest2_2::OnStart";
	end;
}


//----------------------------------------------------------------------------------------------------------------------------------------------------------\\
//				Test 2, Part 1: Resist Temptation			              \\
//********************************************************************************************************************************\\
job_prist.gat,1,1,0	script	PrstTest2_2	-1,{

OnStart:
	enablenpc "Devi_Trig";		//enables these npc's just in case they were disabled by a previous tester
	enablenpc "Deviruchi";
	enablenpc "Doppel_Trig";
	enablenpc "Doppel";
	enablenpc "Dark_Trig";
	enablenpc "Dark Lord";
	enablenpc "Bapho_Trig";
	enablenpc "Baphomet";
	set $@PrstRm, 2;
	set $PRIEST_Q3, 0;
	initnpctimer;
	end;

OnCheck:
	set $PRIEST_Q3, $PRIEST_Q3 + 1;	//this counter checks to see if the player spoke with each devil and answered the questions
	if($PRIEST_Q3 < 4) end;

	enablenpc "prst2_1";
	areaannounce "job_prist.gat", 160, 14, 175, 178, "[Father Peter]: ยอดเยี่ยม ท่านได้แสดงความกล้าหาญที่ยิ่งใหญ่ ข้าจะพาวาร์ปไปยังห้องถัดไป",8;
	initnpctimer "prst2_1";		//starts a 30 sec timer.  this will warp the player even if they don't enter the warp to keep the test going
	end;

OnTimer500:
	stopnpctimer;
	areaannounce "job_prist.gat", 160, 14, 175, 178, "[Father Peter]: การทดสอบนี้จะทดสอบความแข็งแรงและความมุ่งมันของท่านกับพระผู้เป็นเจ้า!",8;
	end;

}


// Deviruchi ------------------------------------------------------------------------
job_prist.gat,168,45,4	script	Deviruchi	738,{

OnStart1:
	if(class == Job_Acolyte) goto L_Aco;

L_Priest:
	mes "[Deviruchi]";
	mes " เฮ้ priest อย่างท่านมาทำอะไรที่นี่?";
	mes " ข้าว่าท่านควรกลับไปที่เมือง ท่านไม่ควรจะมีเสียเวลาเช่นนี้หรอกนะ?";
	close;

L_Aco:
	mes "[Deviruchi]";
	mes " โย่.... acolyte หรือ ไม่ได้เห็นมานาน";
	mes " โอ่.... อยากเป็น Priest หรือ";
	next;
	mes "[Deviruchi]";
	mes " ข้าจะบอกเรื่องที่น่าเศร้าของการเป็นผู้รับใช้พระผู้เป็นเจ้า ";
	mes " ข้าคิดว่ามันแปลกเหมือนกันที่ท่านยังอยู่ที่นี่";
	next;
	mes "[Deviruchi]";
	mes " เส้นทางชีวิตของท่านจะเดินไปด้วยความยากลำบาก มันไม่ใช่อาชีพที่ดีที่ท่านควรจะเป็น?";
	next;
	mes "[Deviruchi]";
	mes " มันไม่สำคัญว่าจะเป็นในเมือง หรือถ้าเพราะทุกๆ ที่ท่านจะต้องช่วยเหลือผู้คน";
	mes " และมันเป็นเรื่องที่น่าตลกที่พวกเขา ไม่เคยหันกลับมาช่วยท่านเลย";
	next;
	mes "[Deviruchi]";
	mes " นี่ข้าพูดจากใจจริงอย่ายอมมแพ้ง่ายๆล่ะ";
	mes " การทดสอบน่ะยากแต่ชีวิตยากซะยิ่งกว่าอีก";
	next;
	menu " งั้นยอมแพ้ดีกว่า",-, " เจ้าปีศาจ ไปให้พ้น",M_1a;

		mes "[Deviruchi]";
		mes " ใช่ มันเป็นที่เลือกที่ดีที่สุด เจ้าได้ไม่ต้องมายังที่นี่อีก เมื่อตัดสินใจยอมแพ้แล้วข้าจะพาเจ้าไปสู่ปัจจุบัน";
		next;
		mes "[Deviruchi]";
		mes " ข้าจะให้เจ้าได้พบเพื่อนของข้า  ฮ่าา ฮ่าา ฮ่าา!";
		next;
		warp "c_tower2.gat", 168, 33;
		end;
	M_1a:
		mes "[Deviruchi]";
		mes " โอ้.. ข้ากลัว ฮ่า ฮ๋า  ท่านจงฟังสิ่งที่ข้ากำลังจะพูด";
		next;
		mes "[Deviruchi]";
		mes " หากเจ้ายอมแพ้ข้าตอนนี้ ข้าจะให้ของขวัญที่หายากที่สุดที่ท่านจะได้";
		next;
		cutin "นฬฝบฦฎทนฝบฤซตๅ.bmp" ,4;
		mes "[Deviruchi]";
		mes " เจ้าอาจไม่มีโอกาสที่จะได้มัน?";
		mes " คิดดีๆ อย่าเลือกทางลำบากเลย...";
		next;
		menu " เอาการ์ดมาให้ข้า",-, " ไอ้ปีศาจ ไปให้พ้น!",sM_1a;

			mes "[Deviruchi]";
			mes " ฮ่า ฮ่า! มนุษย์ทุกคนก็ต้องเลือกางนี้ เอาล่ะข้าจะมอบการ์ดใบนี้ให้เจ้า";
			next;
			cutin "นฬฝบฦฎทนฝบฤซตๅ.bmp" ,255;
			mes "[Deviruchi]";
			mes " ดี ข้าเสียใจข้าให้เจ้าไม่ได้! เจ้าต้องไปตามหามันด้วยตัวเจ้าเอง";
			next;
			warp "mjolnir_05.gat", 200, 200;
			end;
		sM_1a:
			//showimage "นฬฝบฦฎทนฝบฤซตๅ.bmp" 255
			mes "[Deviruchi]";
			mes " ว้าว เจ้าเป็นหนึ่ง acolyte ที่ปฏิเสธข้า เจ้าคงจะมีทางที่ดีกว่า";
			mes " ข้าจะคอยดูเจ้าว่าเจ้าจะกลายเป็น priest ที่ดีได้หรือไม่";
			next;
			mes "[Deviruchi]";
			mes " วันหนึ่งเจ้าจะเสียใจและเจ้าจะต้องกลับมาหาข้า!";
			disablenpc "Devi_Trig";	//disables the trigger npc so you don't accidentally activate it again
			disablenpc "Deviruchi";
			donpcevent "PrstTest2_2::OnCheck";
			close;
}

// Doppelganger ------------------------------------------------------------------------------
job_prist.gat,168,80,4	script	Doppelganger::Doppel	739,{

OnStart1:
	if(class == Job_Acolyte) goto L_Aco;

L_Priest:
	mes "[Doppelganger]";
	mes "What good is it to be here?.";
	mes "Are you this bored? you're already a priest why are you here?.";
	next;
	mes "[Doppelganger]";
	mes "You don't need to help these acolytes, go home.";
	mes "You're doing something that is wasting your time, leave now.";
	close;
L_Aco:
	mes "[Doppelganger]";
	mes " เฮ้เจ้า Acolyte ฟังคำพูดข้าซะ";
	next;
	mes "[Doppelganger]";
	mes " ที่เจ้ามาที่นี่เจ้าคงจะต้องการจะเป็น priest สินะ?";
	mes " ข้าว่าคนอย่างเจ้าไม่ควรจะเป็น priest หรอก";
	next;
	mes "[Doppelganger]";
	mes " หากเจ้าต้องการพลัง ข้าจะสามารถใช้อำนาจของข้าเพื่อให้เจ้าได้เริ่มต้นใหม่อีกครั้ง";
	mes " เจ้าจะกลายเป็น novice และเจ้าจะได้เลือกอาชีพที่เจ้าต้องการได้ใหม่";
	next;
	mes "[Doppelganger]";
	mes " แน่นอนว่าข้าจะมอบ skill ให้ด้วย";
	mes " นี่เป็นโอกาสที่ดีนะ";
	next;
	menu " เอาสิข้าต้องการ!",-, " ไอ้ปีศาจ ไปให้พ้น!",M_1b;

		mes "[Doppelganger]";
		mes " ใช่นี่เป็นทางที่ดี";
		mes " ข้าจะทำให้เจ้าเป็น novice เดี๋ยวนี้";
		next;
		mes "[Doppelganger]";
		mes " แต่มันต้องใช้เวลานาน หลังเจ้าที่เจ้าได้ตายไปแล้ว!!";
		next;
		warp "gef_dun02.gat", 210, 177;
		end;
	M_1b:
		mes "[Doppelganger]";
		mes " ข้าคิดว่าเจ้าคงจะไม่เข้าใจความหมายที่ข้าพูดหรอกนะ";
		mes " นี่อาจจะเป็นสิ่งดีที่สุด ที่จะเกิดในชีวิตเจ้า";
		next;
		mes "[Doppelganger]";
		mes " เจ้าเพียงแค่บอกข้าว่าเจ้าไม่อยากเป็น priest";
		mes " ข้าจะทำให้เจ้าสามารถเลือกเส้นทางชีวิตใหม่ได้ หรือสิ่งที่เจ้าต้องการจะเป็น swordman แบบข้า?";
		next;
		menu " ข้าไม่อยากเป็น priest!",-, " ไอ้ปีศาจ ไปให้พ้น!",sM_1b;

			mes "[Doppelganger]";
			mes " ใช่นี่เป็นทางที่ดี";
			mes " ข้าจะทำให้เจ้าเป็น novice เดี๋ยวนี้";
			next;
			mes "[Doppelganger]";
			mes " แต่มันต้องใช้เวลานาน หลังเจ้าที่เจ้าได้ตายไปแล้ว!!";
			next;
			warp "gef_dun02.gat", 210, 177;
			end;
		sM_1b:
			mes "[Doppelganger]";
			mes " โอเค ข้าจะรอวันนี้อีกครั้ง";
			next;
			mes "[Doppelganger]";
			mes " ถ้าข้าเห็นเจ้าอีก ข้าจะทำให้เจ้าตายอย่างทรมาน";
			disablenpc "Doppel_Trig";
			disablenpc "Doppel";
			donpcevent "PrstTest2_2::OnCheck";
			close;
}

// Dark Lord -------------------------------------------------------------------------------
job_prist.gat,168,115,4	script	Dark Lord	737,{

OnStart1:
	if(class == Job_Acolyte) goto L_Aco;

L_Priest:
	mes "[Dark Lord]";
	mes " ข้าจะให้เจ้ารู้สึกถึงความเกลียดและความโกรธ!!";
	mes " ข้าจะให้เจ้ารู้สึกเหมือนเพื่อนที่ทรยศเจ้า!";
	next;
	mes "[Dark Lord]";
	mes " อยู่ที่นี่กับข้า ข้าจะพาเจ้าฝึกฝนจนเจ้ามีความเข้มแข็วเพียงพอแล้วเจ้าก็กลับไป!";
	mes " อยู่ที่นี่กัยข้า ข้าสอนให้เจ้ารู้วีธีสาปแช่ง!";
	close;
L_Aco:
	mes "[Dark Lord]";
	mes " หยุดซะ เจ้ามนุษย์!.";
	mes " ใครอนุญาติให้เจ้าผ่านที่นี่ได้!";
	next;
	mes "[Dark Lord]";
	mes " ถ้าเจ้าต้องการเป็น priest เจ้าก็จะไม่สามารถผ่านที่นี่ไปได้";
	mes " ไปจากที่นี่ซะก่อนที่ข้าจะฆ่าเจ้า";
	next;
	mes "[Dark Lord]";
	mes " ก่อนข้าจะฉีกร่างของเจ้าด้วยเวทมนตร์ของข้า ไปให้พ้น!";
	next;
	menu " โอ้ ได้โปรดอย่าฆ่าข้าเลย",-, " เจ้าปีศาจไปให้พ้น!.",M_1c;

		mes "[Dark Lord]";
		mes " อย่ามาที่นี่อีก!";
		next;
		warp "gl_church.gat", 145, 170;
		end;
	M_1c:
		mes "[Dark Lord]";
		mes " เจ้าจงอย่าทำตัวเช่นคนก้าวร้าว";
		mes " ข้าสามารถตัดเจ้าด้วยมือเพียงข้างเดียวของข้า";
		next;
		mes "[Dark Lord]";
		mes " ไปจากที่นี่ ก่อนที่ข้าจะให้พลังแห่งความมืดครอบงำเจ้า!";
		next;
		menu " ได้โปรด ปล่อยข้าไป",-, " เจ้าปีศาจไปให้พ้น!",sM_1c;

			mes "[Dark Lord]";
			mes " อย่ามาที่นี่อีก!";
			next;
			warp "gl_church.gat", 145, 170;
			end;
		sM_1c:
			mes "[Dark Lord]";
			mes " เจ้ายังคิดจะอยู่ที่นี่อีก.. มนุษย์ที่น่ารำคาญอย่างเจ้าหากข้าฆ่าเจ้าไป";
			mes " มือข้าก็มีแต่จะสกปรก!!";
			next;
			mes "[Dark Lord]";
			mes " อย่าให้ข้าเห็นเจ้าอีกแล้วกัน ข้าจะฆ่าเจ้า";
			disablenpc "Dark_Trig";
			disablenpc "Dark Lord";
			donpcevent "PrstTest2_2::OnCheck";
			close;
}

// Baphomet -----------------------------------------------------------------------------
job_prist.gat,168,150,4	script	Baphomet	736,{

OnStart1:
	if(class == Job_Acolyte) goto L_Aco;

L_Priest:
	mes "[Baphomet]";
	mes " Priests ที่น่ารำคาญ";
	next;
	mes "[Baphomet]";
	mes " ข้าไม่มีอะไรจะพูดกับเจ้า ไปซะ.";
	close;
L_Aco:
	mes "[Baphomet]";
	mes " เฮ้ เจ้ามนุษย์";
	next;
	mes "[Baphomet]";
	mes " เจ้าสนใจในข้อตกลงของข้ามั้ย?";
	next;
	mes "[Baphomet]";
	mes " ข้าสามารถมอบความร่ำรวยและความมีชื่อเสียงในโลกนี้ให้เจ้าได้";
	mes " เงินและอาวุธที่ไม่มีผู้ใดสามารถครอบครอง...";
	next;
	mes "[Baphomet]";
	mes " นอกจากนี้ ท่านยังสามารถเรียกข้าไปหาเจ้าได้หากต้องการ";
	mes " มันจะทำให้ทุกๆ คนกลัวเจ้า ฮ่า ฮ่า ฮ๋า";
	next;
	mes "[Baphomet]";
	mes " ยอมแพ้และทำสัญญากับข้าเถอะ";
	mes " แล้วโลกจะเป็นของเจ้า";
	next;
	menu" ข้าไม่อยากเป็น priest แล้ว",-, " ไอ้แพะโง่ ไปให้พ้น!",M_1d;

		mes "[Baphomet]";
		mes " โอเค เรามาเซ็นสัญญา";
		mes " เจ้าจะไม่ลืมสิ่งนี้...";
		next;	
		mes "[Baphomet]";
		mes " เจ้าต้องไปหาข้า";
		mes " เมื่อเจอข้า ให้มาหาข้าและมาเซ็นสัญญากับข้า";
		next;
		warp "glast_01.gat", 200, 203;
		end;
	M_1d:
		mes "[Baphomet]";
		mes " โอเค ข้าจะออกไป";
		mes " แต่ข้าจะไม่ปล่อยเจ้าไปง่ายๆ หรอก";
		next;
		mes "[Baphomet]";
		mes " แล้วข้าจะมอบสิ่งชัวร้ายมาให้เจ้า...";
		mes " ข้าจะคอยดูระยะเวลาสุดท้ายของชีวิตเจ้า";
		next;
		mes "[Baphomet]";
		mes " โอเค ไปเดี๋ยวนี้";
		disablenpc "Bapho_Trig";
		disablenpc "Baphomet";
		donpcevent "PrstTest2_2::OnCheck";
		close;
}


// These hidden warps trigger the npcs when you get near them
// Deviruchi Trigger --------------------------------------------------------------------
job_prist.gat,167,40,1	script	Devi_Trig	139,8,0,{

	doevent "Deviruchi::OnStart1";
	end;
}
// Doppleganger Trigger --------------------------------------------------------------------
job_prist.gat,167,77,1	script	Doppel_Trig	139,8,0,{

	doevent "Doppel::OnStart1";
	end;
}
// Dark Lord Trigger --------------------------------------------------------------------
job_prist.gat,167,112,1	script	Dark_Trig	139,8,0,{

	doevent "Dark Lord::OnStart1";
	end;
}
// Baphomet Trigger --------------------------------------------------------------------
job_prist.gat,167,145,1	script	Bapho_Trig	139,8,0,{

	doevent "Baphomet::OnStart1";
	end;
}


// End warp for 2nd part of test ---------------------------------------------------------
job_prist.gat,168,180,0	script	prst2_1	45,3,3,{

	if(class == Priest) end;		//If a Priest friend steps on the warp nothing happens.  Need the Aco to step on the warp.
OnTimer30000:
	set $@PrstRm, 3;
	set $PRIEST_Q3, 0;
	enablenpc "Mummy1_Trig";		//enables the floor triggers for the next test
	enablenpc "Mummy2_Trig";
	enablenpc "Mummy3_Trig";
	stopnpctimer;
	initnpctimer "prst3_1";
	areawarp "job_prist.gat", 160, 14, 175, 178, "job_prist.gat", 98, 40;
	end;
}


//----------------------------------------------------------------------------------------------------------------------------------------------------------\\
//				Test 2, Part 3: Make it to the exit 			               \\
//******************************************************************************************************************************* *\\
// First set of Mummies --------------------------------------------------------
job_prist.gat,97,50,0	script	Mummy1_Trig	139,8,1,{

	monster "job_prist.gat", 93, 55, "ca", 1041,1;
	monster "job_prist.gat", 102, 55, "ah", 1041,1;
	disablenpc "Mummy1_Trig";
	end;
}
// Second set of Mummies --------------------------------------------------------
job_prist.gat,97,65,0	script	Mummy2_Trig	139,8,1,{

	monster "job_prist.gat", 93, 70, "men", 1041,1;
	monster "job_prist.gat", 102, 70, "ahke", 1041,1;
	disablenpc "Mummy2_Trig";
	end;
}
// Third set of Mummies --------------------------------------------------------
job_prist.gat,97,80,0	script	Mummy3_Trig	139,8,1,{

	monster "job_prist.gat", 93, 85, "mea", 1041,1;
	monster "job_prist.gat", 102, 85, "sne", 1041,1;
	disablenpc "Mummy3_Trig";
	end;
}


// End warp for 3rd part of test ----------------------------------------------
job_prist.gat,98,105,0	script	prst3_1	45,2,2,{

	if(class == Priest) end;		//If a Priest friend steps on the warp nothing happens.  Need the Aco to step on the warp.
	stopnpctimer "TimerPrst";
	areawarp "job_prist.gat", 90, 34, 105, 105, "prt_church.gat",16, 37;
	set PRIEST_Q2, 2;
	killmonsterall "job_prist.gat";
	donpcevent "Father Peter::OnStart";
	end;

OnTimer500:
	stopnpctimer;
	areaannounce "job_prist.gat",  90, 34, 105,105, "[Father Peter]: นี่คือการทดสอบสุดท้ายที่ต้องเผชิญกับลูกของข้า ข้าเชื่อว่าท่านจะผ่านมันไปได้!",8;
	end;
}


//----------------------------------------------------------------------------------------------------------------------------------------------------------\\
//					Timer					              \\
//********************************************************************************************************************************\\
job_prist.gat,1,1,0	script	TimerPrst	-1,{

OnTimer3000:
	areaannounce "job_prist.gat",8,34,39,109,"[Father Peter]: ข้าให้ท่าน 5 นาที! 5 นาทีเท่านั้น! ไปจัดการพวกมันซะ",8;
	end;
OnTimer33000:
	donpcevent "Father Peter::OnStart";
	end;
OnTimer63000:
	set $@PrstTime$, "มีเวลา 4 นาทีสุดท้าย!";
	donpcevent "Father Peter::OnStart";
	goto AnnouncePrst;
OnTimer93000:
	donpcevent "Father Peter::OnStart";
	end;
OnTimer123000:
	set $@PrstTime$, "มีเวลา 3 นาทีสุดท้าย!";
	donpcevent "Father Peter::OnStart";
	goto AnnouncePrst;
OnTimer153000:
	donpcevent "Father Peter::OnStart";
	end;
OnTimer183000:
	set $@PrstTime$, "มีเวลา 2 นาทีสุดท้าย!";
	donpcevent "Father Peter::OnStart";
	goto AnnouncePrst;
OnTimer213000:
	donpcevent "Father Peter::OnStart";
	end;
OnTimer243000:
	set $@PrstTime$, "มีเวลา 1 นาทีสุดท้าย!";
	donpcevent "Father Peter::OnStart";
	goto AnnouncePrst;
OnTimer273000:
	set $@PrstTime$, "มีเวลา 30 วินาทีสุดท้าย!";
	donpcevent "Father Peter::OnStart";
	goto AnnouncePrst;
OnTimer293000:
	set $@PrstTime$, "มีเวลา 10 วินาทีสุดท้าย!";
	goto AnnouncePrst;
	end;
OnTimer2970000:
	set $@PrstTime$, "มีเวลา 5 วินาทีสุดท้าย";
	goto AnnouncePrst;
	end;
OnTimer302000:
	set $@PrstTime$, "หมดเวลา ท่านล้มเหลวในการทดสอบ";
	goto AnnouncePrst;
OnTimer306000:
	stopnpctimer;
	if($@PrstRm==1) areawarp "job_prist.gat",8,34,39,109,"prt_church.gat",16, 37;
	if($@PrstRm==2) areawarp "job_prist.gat",160, 14, 175, 178,"prt_church.gat",16, 37;
	if($@PrstRm==3) areawarp "job_prist.gat", 90, 34, 105,105,"prt_church.gat",16, 37;
	killmonsterall "job_prist.gat";
	donpcevent "Father Peter::OnStart";
	end;

AnnouncePrst:
	if($@PrstRm==1) areaannounce "job_prist.gat",8,34,39,109,"[Father Peter]: "+$@PrstTime$+".",8;
	if($@PrstRm==2) areaannounce "job_prist.gat",160, 14, 175, 178, "[Father Peter]: "+$@PrstTime$+".",8;
	if($@PrstRm==3) areaannounce "job_prist.gat",  90, 34, 105,105, "[Father Peter]: "+$@PrstTime$+".",8;
	end;
}



//**********************************************************************************************************************************************************************************\\
//================================================= Sister Cecile: Test 3 =====================================================\\
//**********************************************************************************************************************************************************************************\\
prt_church.gat,27,24,4	script	Sister Cecile	79,{
	mes "[Sister Cecile]";
	if(Class == Job_Priest) goto L_Prst;
	if(Class == Job_Acolyte) goto L_Aco;
	if(Class == Job_Novice) goto L_Nov;

L_Other:
	mes " นี่คือโบถส์แห่ง Prontera ท่านมีอะไรให้ข้าช่วย?";
	next;
	menu " บอกข้าเกี่ยวกับ priests.",-, " ข้าแค่เดินไปรอบๆ",M_End0;

		mes "[Sister Cecile]";
		mes " Priests เป็นผู้รับใช้ของพระเจ้า";
		mes " เขามีอำนาจพิเศษที่จะช่วยเหลือทุกๆ คนที่ต้องการความช่วยเหลือ";
		next;
		mes "[Sister Cecile]";
		mes " เพราะพวกเขาเป็นศิษของพระเจ้า พวกเขาไม่สามารถใช้ดาบหรืออาวุธประเภทอื่น ๆ ได้";
		next;
		mes "[Sister Cecile]";
		mes " และหากท่านยังอยากรู้เรื่องราวของ Priest เพิ่มเติม เจ้าต้องไปพูดคุยกับคนในโบสถ์เขาจะบอกเจ้า";
		close;
	M_End0:
		mes "[Sister Cecile]";
		mes " การที่เจ้ามาเดินแถวนี้ ไม่ได้ทำให้เจ้าเหน็ดเหนื่อยหรอกนะ";
		close;

L_Nov:
	mes " พระเจ้าจงสถิตกับลูกของข้า ยินดีต้อนรับเจ้า Novice";
	next;
	mes "[Sister Cecile]";
	mes " เจ้าคงอยากจะเป็นหนึ่งในผู้รับใช้พระเจ้าสินะ?";
	mes " การเป็น Acolyte คือการช่วยเหลือผู้อื่นมันเป็นสิ่งที่ดีมาก";
	next;
	mes "[Sister Cecile]";
	mes " หากเจ้าอยากเป็น Acolyte เจ้าจงไปพูดคุยกับ ^5533FFบาทหลวง Mareusis^000000 ในห้องรูปไม้กางเขน";
	next;
	mes "[Sister Cecile]";
	mes " และเมื่อเจ้ามีระดับจ๊อบเลเวล 40 Acolyte อย่างเจ้าก็สามารถกลับมาที่นี่เพื่อเปลี่ยนเป็น Priest ได้";
	next;
	mes "[Sister Cecile]";
	mes " ต้้งแต่วันนี้เจ้า novice จงกลับไปคิดให้รอบคอบกับสิ่งที่เจ้าต้องการอยากจะเป็น";
	close;

L_Prst:
	if(sex == 1) mes "พระองค์จะสถิตกับท่าน";
	if(sex == 0) mes "พระองค์จะสถิตกับท่าน";
	mes "ข้าขอให้เจ้าทำทั้งหมดให้ดีที่สุด ด้วยความเชื่อของเจ้า พระเจ้าจะทรงรักเจ้าเสมอ..";
	close;

L_Aco:
	if(PRIEST_Q == 1) goto L_Test1;
	if(PRIEST_Q == 2) goto L_Test2;
	if(PRIEST_Q == 3) goto L_Start;
	if(PRIEST_Q == 4) goto L_Done;
	if(sex == 1) mes "พระองค์จะสถิตกับท่าน";
	if(sex == 0) mes "พระองค์จะสถิตกับท่าน";
	mes "เจ้ามาทำอะไรที่นี่?";
	next;
	menu " ข้าต้องการเป็น priest.",-, " ข้าแค่มาเดินรอบๆ",M_End1;

		mes "[Sister Cecile]";
		mes " โอ้ เจ้าต้องการจะเป็น priest?";
		mes " มี acolytes จำนวนมากที่ต้องการเช่นเจ้า";
		next;
		mes "[Sister Cecile]";
		mes " ข้าคือ Sister Cecile ข้ามีหน้าที่รับผิดชอบให้ acolytes เปลี่ยนเป็น priests.";
		next;
		mes "[Sister Cecile]";
		mes " เพราะข้าเติบโตที่นี่ข้าได้เห็นคนจำนวนมากได้เปลี่ยนเป็น Priests มาหลายปี";
		next;
		mes "[Sister Cecile]";
		mes " ก่อนที่จะเป็น priest เจ้าจะต้องทำงานที่ได้รับมอบหมาย 3 งาน  หากเจ้าต้องการ เจ้าต้องไปพูดคุยกับ Father Thomas,";
		next;
		mes "[Sister Cecile]";
		mes " หากเจ้ามีปัญหาใด ๆ ก็กลับมาหาข้า ข้าจะคอยช่วยเหลือท่านได้";
		close;
	M_End1:
		mes "[Sister Cecile]";
		mes " เจ้าจงผ่อนคลายและคิดว่าที่นี่คือบ้านของเจ้า";
		close;

L_Start:
	if(PRIEST_Q2 == 1) goto L_ReTest;
	mes " เยี่ยม ตอนนี้เจ้าก็ผ่านการทดสอบมาทั้ง 2 การทดสอบแล้ว!";
	mes " ความฝันที่จะเป็น priest ของเจ้าใกล้จะเป็นความจริงแล้ว";
	next;
	mes "[Sister Cecile]";
	if(sex==1) mes " งั้นข้าจะเริ่มการสาบานตนเพื่อที่จะได้เป็น priest";
	if(sex==0) mes " งั้นข้าจะเริ่มการสาบานตนเพื่อที่จะได้เป็น priest";
	mes " นี่เป็นเรื่องง่านมาก แค่พูดความจริงก็พอ";
	next;
	mes "[Sister Cecile]";
	mes " เอาล่ะ ข้าจะถามคำถามเจ้าบางอย่าง";
	set PRIEST_Q2, 1;
	next;
L_Test3:
	mes "[Sister Cecile]";
	mes "ท่านจะรับใช้พระเจ้าไปชั่วชีวิตไหม?";
	next;
	menu "แน่นอน",M_2a, "ไม่",-;

		mes "[Sister Cecile]";
		mes " ทำไม ทำไม ทำไมเจ้าจึงตอบเช่นนั้น?...";
		mes " ข้าคิดว่าเจ้าไม่เหมาะกับการเป็น priest...";
		close;

	M_2a:

	mes "[Sister Cecile]";
	mes "ท่านจะใช้ skill เพียงเพื่อตัวท่านเองหรือไม่";
	next;
	menu "แน่นอน",-, "ไม่",M_2b;

		mes "[Sister Cecile]";
		mes " ไม่นี่มันไม่ใช่สิ่งสำคัญของ priest ที่เจ้าควรเจ้าจะทำ";
		close;

	M_2b:


	mes "[Sister Cecile]";
	mes "ท่านจะใช้ skill ของท่านเพื่อผู้ที่เผชิญอันตรายในการต่อสู้กับสัตว์ประหลาดหรือไม่?";
	next;
	menu "ใช่",M_2c, "ไม่",-;

		mes "[Sister Cecile]";
		mes " นี่เป็นทางเลือกที่ผิด หากมีคนต้องการความช่วยเหลือ เจ้าจะต้องทำให้ดีที่สุดเพื่อช่วยเขา";
		mes " มันไม่สำคัญว่านั่นคืองานของเราหรือไม่ แต่หน้าที่ของเราคือช่วยเหลือทุ ๆ คน";
		close;
	M_2c:

	mes "[Sister Cecile]";
	mes "ท่านจะช่วยเหลือผู้อื่น และเสียสละเพื่อผู้อื่นไหม?";
	next;
	menu "ใช่",M_2d, "ไม่",-;

		mes "[Sister Cecile]";
		mes " ไม่ นี่ไม่ใช่สิ่งที่พระเจ้าทรงคิด..";
		mes " ถ้าเราสามารถเสียสละตัวเองเพื่อช่วยโลกได้่ นั่นมันคือสิ่งที่คุ้มค่าที่สุด";
		close;
	M_2d:

	mes "[Sister Cecile]";
	mes "ท่านจะพูดคำพูดเหมือนเดิมในเมืองหรือหมู่บ้านไหม?";
	next;
	menu "ใช่",-, "ไม่",M_2e;

		mes "[Sister Cecile]";
		mes " นั่นคือสิ่งที่ไม่ถูกต้อง priests อย่างเรามีหน้าที่ช่วยเหลือผู้คน";
		close;

	M_2e:

	mes "[Sister Cecile]";
	mes "ท่านจะช่วยเหลือผู้อื่นในการล่าโดยการลากสัตว์ประหลาดไปให้พวกเขาไหม?";
	next;
	menu "ใช่",-, "ไม่",M_2f;

		mes "[Sister Cecile]";
		mes " เจ้าไม่สามารถทำแบบนั้น";
		mes " priests อย่างเราไม่ควรอยู่ข้างเดียวกันกับพวกปีศาจ";
		close;

	M_2f:

	mes "[Sister Cecile]";
	mes "ท่านจะอยู่และตายตามพระผู้เป็นเจ้าไหม?";
	next;
	menu "ใช่",M_2g, "ไม่",-;

		mes "[Sister Cecile]";
		mes " หากเช่นนั้นเจ้าก็ไม่สมควรจะเป็น priest.";
		mes " ถ้าพระเจ้าต้องการให้เจ้าตายเจ้าก็จะต้องรับฟังเขา";
		close;
	M_2g:

	mes "[Sister Cecile]";
	mes "ข้าได้ยินคำสาบานตนของท่านแล้ว ท้ายที่สุด ท่านต้องรักษาสัญญาที่มอบไว้กับข้า";
	next;
	menu "ข้าสาบาน",-, "ไม่",M_2No;

		mes "[Sister Cecile]";
		mes " ตอนนี้ท่านผ่านการทดสอบทั้งหมด";
		mes " กับไปพูดคุยกับ บาทหลวง Thomas เพื่อเปลี่ยนเป็น priest.";
		next;
		mes "[Sister Cecile]";
		mes " จงอย่าลืมคำพูดที่ท่านได้พูดทั้งหมดในวันนี้";
		mes " พระเจ้าจะอวยพรท่าน...";
		set PRIEST_Q, 4;
		set PRIEST_Q2, 0;
		close;

	M_2No:
		mes "[Sister Cecile]";
		mes "..............";
		next;
		mes "[Sister Cecile]";
		mes " ท่านตอบว่าไม นี่ท่านยังลังเลที่จะเป็น priest อีกงั้นเหรอ";
		close;

L_ReTest:
	mes ".............";
	next;
	mes "[Sister Cecile]";
	mes "...โอ้ ท่านกลับมาแล้ว";
	mes " ข้าหวังว่าครั้งนี้ท่านจะพูดความจริง";
	next;
	mes "[Sister Cecile]";
	mes " ข้าจะสามารถรู้สึกความคิดของท่านด้วยความจริงใจ...";
	mes " กรุณาตอบอย่างตรงไปตรงมา";
	next;
	mes "[Sister Cecile]";
	mes " โอเค เริ่มได้";
	next;
	goto L_Test3;

L_Test1:
	mes " โอ้ ท่านยังไม่ได้เริ่มการทดสอบขั้นแรก.. !!";
	close;

L_Test2:
	if(PRIEST_Q2 == 1) goto L_Test2a;
	mes " นี่ท่านยังไม่ได้เริ่มทำบทสอบบทสอบที่ 2?.";
	mes " หากท่านต้องการรายละเอียดเกี่ยวกับงานนี้ให้ท่านไปหา";
	mes " บาทหลวง Thomas เข้าเป็นคนดีมาก เขาจะคอยช่วยท่าน";
	close;

L_Test2a:
	mes " แต่บททดสอบที่ยาก ที่ท่านจงอย่ายอมแพ้";
	mes " ท่านต้องเอาชนะมันให้ได้ เพื่อที่ท่านจะเป็น priest.";
	next;
	mes "[Sister Cecile]";
	mes " หากมี priest ที่มีประสบการณ์พงกเขาจะสามารถช่วยเหลือการทดสอบนี้ได้";
	mes " พวกเขาอาจจะทำให้ท่านผ่านการทดสอบที่ 1 และ 2 อย่างง่ายๆ ได้";
	next;
	mes "[Sister Cecile]";
	mes "God bless you...";
	mes " หลังจากทำบททดสอบเสร็จทั้งหมดแล้ว ค่อยกลับมาหาข้า";
	close;

L_Done:
	mes " ยินดีด้วยท่านผ่านการทดสอบทั้งหมดแล้ว กลับไปหา บทหลวง Thomas เขาสามารถทำให้ท่านเปลี่ยนเป็น Priest";
	close;
}
