alde_alche,24,188,3	script	Guild Dealer	740,{
	if (checkweight(1201,1) == 0) {
		mes "- เจ้าไม่สามารถทำได้... -";
		mes "- น้ำหนักในตัวละครมากกว่าที่กำหนด -";
		mes "- กลับไป.... -";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "ยินดีต้อนรับ...";
	mes "ท่านมาที่แห่งนี้....";
	mes "วันนี้ท่านต้องการทำอะไร..?";
	next;
	switch(select("ซื้อ Medicine Bowl:ซื้อ หนังสือปรุงยา")) {
	case 1:
		mes "[Gever Al Sharp]";
		mes "คุณต้องการเลือกอะไร..?";
		next;
		if (select("Medicine Bowl - 8 Zeny:ยกเลิก") == 1) {
			mes "[Gever Al Sharp]";
			mes "แล้วคุณต้องการจำนวนเท่าไหร่?";
			mes "ถ้าใส่ '0' คือต้องการออกจากระบบ";
			next;
			while(1) {
				input .@input,0,2001;
				if (.@input == 0) {
					mes "[Gever Al Sharp]";
					mes "จริง ๆ แล้วฉันต้องการยกเลิก";
					mes "ขอเป็นโอกาสหน้าละกัน...";
					close;
				}
				else if (.@input < 1 || .@input > 2000) {
					mes "[Gever Al Sharp]";
					mes "ในการแลกขวดนั้น..";
					mes "ไม่สามารถแลกได้เกิน 2000 ขวด";
					next;
				}
				else {
					break;
				}
			}
			set .@sell,.@input * 8;
			set .@item_weight,.@input * 10;
			if (Zeny < .@sell) {
				mes "[Gever Al Sharp]";
				mes "คุณไม่สามารถซื้อได้";
				mes "เนื่องจากเงินของคุณมีไม่เพียงพอ";
				mes "โปรดกลับไปเช็คเงินในตัวละครคุณเสียก่อน";
				mes "แล้วค่อยกลับมาหาฉัน..";
				close;
			}
			if ((MaxWeight - Weight) < .@item_weight) {
				mes "[Gever Al Sharp]";
				mes "คุณไม่สามารถทำรายการนี้ได้";
				mes "เนื่องจากตัวละครของท่าน..";
				mes "มีของหรือน้ำหนักที่มากเกินกว่ากำหนด";
				mes "ไปเก็บของที่ Kafra ก่อนสิ..";
				close;
			}
			set zeny,zeny-.@sell;
			getitem 7134,.@input; //Medicine_Bowl
			mes "[Gever Al Sharp]";
			mes "ขอบคุณมาก..";
			mes "แล้วกลับมาหาใหม่น่ะ";
			close;
		}
		mes "[Gever Al Sharp]";
		mes "แล้วแต่ท่านน่ะ..";
		mes "ถ้าคิดจะเปลี่ยนใจ..";
		mes "ก็มาหาฉันได้ที่นี่..";
		mes "ฉันรอคุณอยู่เสมอเพราะ..";
		mes "ฉันเฉ๋ง...?";
		close;

	case 2:
		mes "[Gever Al Sharp]";
		mes "What do you need?";
		mes "Manuals are generally 100,000 zeny. But there are a couple of special manuals that will cost more.";
		next;
		switch(select("Potion Creation Guide:Alcohol Creation Guide:Bottle Grenade Creation Guide:Acid Bottle Creation Guide:Plant Bottle Creation Guide:Marine Sphere Bottle Creation Guide:Glistening Coat Creation Guide:Condensed Potion Creation Guide:Cancel Deal.")) {
		case 1:
			callsub S_SellManual,7144,100000; //Normal_Potion_Book
			break;
		case 2:
			callsub S_SellManual,7127,100000; //Alcol_Create_Book
			break;
		case 3:
			callsub S_SellManual,7128,100000; //FireBottle_Create_Book
			break;
		case 4:
			callsub S_SellManual,7129,100000; //Acid_Create_Book
			break;
		case 5:
			callsub S_SellManual,7130,100000; //Plant_Create_Book
			break;
		case 6:
			callsub S_SellManual,7131,100000; //Mine_Create_Book
			break;
		case 7:
			callsub S_SellManual,7132,100000; //Coating_Create_Book
			break;
		case 8:
			callsub S_SellManual,7133,240000; //Slim_Potion_Create_Book
			break;
		case 9:
			mes "[Gever Al Sharp]";
			mes "Well then...";
			mes "Come back if you";
			mes "ever need to buy";
			mes "a production manual.";
			close;
		}
	case 3:
		mes "[Gever Al Sharp]";
		mes "Alright then,";
		mes "have a good day.";
		close;
	}

S_SellManual:
	if (Zeny < getarg(1)) {
		mes "[Gever Al Sharp]";
		mes "You don't";
		mes "have enough zeny.";
		mes "Check how much zeny";
		mes "you have first.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes ""+getitemname(getarg(0))+"?";
	mes "That'll be "+getarg(1)+" zeny.";
	next;
	if (select("Purchase.:Quit.") == 1) {
		set zeny,zeny-getarg(1);
		getitem getarg(0),1;
		mes "[Gever Al Sharp]";
		mes "Thank you for";
		mes "your patronage.";
		close;
	}
	mes "[Gever Al Sharp]";
	mes "Come again";
	mes "next time.";
	close;
}