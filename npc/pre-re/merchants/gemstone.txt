//===== eAthena Script ======================================= 
//= Gemstone trader
//===== By: ================================================== 
//= L0ne_W0lf
//===== Current Version: ===================================== 
//= 1.2
//===== Compatible With: ===================================== 
//= eAhena SVN
//===== Description: ========================================= 
//= [Aegis Conversion]
//= Trade various colors of gemstones for other color gemstones.
//===== Additional Comments: ================================= 
//= 1.0 Rescripted to Aegis 10.3 standards. [L0ne_W0lf]
//=	Any notes pertaining to the prior trader may be found
//=	in the cities/payon.txt
//= 1.1 Corrected NPC names to fall within proper restrictions. [L0ne_W0lf]
//= 1.2 Updated input with min/max values. [L0ne_W0lf]
//=	Added a checkweight. 
//= แปลไทย by GPI-RO
//============================================================ 

payon,99,134,5	script	Gemstones#pay	754,{
	if (checkweight(1201,1) == 0) {
		mes "^3355FFรอสักครู่! เอาล่ะ คุณแบกไอเทมมากเกินไป กลับมาใหม่เมื่อท่านนำไอเทมไปฝากไว้กับ Kafra Storage.^000000";
		close;
	}
	mes "[Jade]";
	mes "ถ้าเจ้านำ";
	mes "Gemstones 2 เม็ด";
	mes "สีเดียวกันมาให้ข้า";
	mes "ข้าจะทำ Gemstones";
	mes "สีอื่นที่เจ้าต้องการ";
	next;
	switch(select("Blue Gemstones เป็น Red!:Red Gemstones เป็น Yellow!:Yellow Gemstones เป็น Blue!")) {
	case 1: callsub S_TradeGems,717,716;
	case 2: callsub S_TradeGems,716,715;
	case 3: callsub S_TradeGems,715,717;
	}

S_TradeGems:
	if (countitem(getarg(0)) < 2) {
		mes "[Jade]";
		mes "ฮ่า...!";
		mes "เจ้าล้อเล่นใช่มั้ย?";
		mes "ข้าไม่สามารถช่วยเจ้าได้";
		mes "ถ้าเจ้ามี Gemstone";
		mes "นำมาให้ข้าน้อยกว่า";
		mes "2 "+getitemname(getarg(0))+"s!";
		close;
	}
	else {
		set .@gems,countitem(getarg(0))/2;
		mes "[Jade]";
		mes "ข้าเชื่อว่าข้าทำได้";
		mes "ถ้าเจ้าจะแลก " + .@gems + " " + getitemname(getarg(1)) + "s";
		mes "เจ้ามีอยู่ "+getitemname(getarg(0))+"s";
		mes "ที่คุณมีอยู่";
		mes "เจ้าต้องกาแลกเท่าไรล่ะ?";
		next;
		switch(select("แลกทั้งหมด:ระบุจำนวน:ออก")) {
		case 1:
			delitem getarg(0),.@gems * 2;
			getitem getarg(1),.@gems;
			mes "[Jade]";
			mes "เจ้าจะรู้สึก";
			mes "อยากกลับมา";
			mes "ทุกเวลา";
			mes "หืม..เจ้ามองอะไรอยู่น่ะ?";
			mes "มีอะไรบนหน้าของข้า?";
			close;
		case 2:
			mes "[Jade]";
			mes "เอาเท่าไรล่ะ";
			mes "เจ้าต้องการเท่าไร?";
			mes "สูงสุดคือ 100 เม็ด";
			next;
			while(1) {
				input .@input,0,101;
				if (.@input == 0) {
					mes "[Jade]";
					mes "ไม่ใส่อะไรเลย?";
					mes "ข้าแนะนำว่า";
					mes "ใเจ้าคิดใหม่...";
					close;
				}
				else if (.@input > 100) {
					mes "[Jade]";
					mes "อืม...";
					mes "ข้าคิดว่าเจ้าาน";
					mes "ใส่ตัวเลขมากกว่า";
					mes "จำนวน 100 เม็ด จำได้มั้ย...?";
					next;
				}
				else {
					break;
				}
			}

			if (.@gems > .@input) {
				delitem getarg(0),.@input * 2;
				getitem getarg(1),.@input;
				mes "[Jade]";
				mes "นี่สำหรับเจ้า";
				mes "เจ้าจะรู้สึกอยากกลับมา";
				mes "หาข้าทุกเวลา";
				mes "เจ้ามองอะไรอยู่น่ะ?";
				mes "มีอะไรบนหน้าข้างั้นรึ?";
			}
			close;
		case 3:
			mes "[Jade]";
			mes "โอเค ไม่มีปัญหา";
			mes "แล้วกลับมาใหม่ละกัน";
			close;
		}
	}
}
