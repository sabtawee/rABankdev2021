//=====  Script =======================================
//= Stone Exchanger
//===== By: ==================================================
//= Bankdev

morocc,160,95,4	script	Stone Vendor Machine#pa08	562,{
	if (checkweight(1201,1) == 0) {
		mes "คุณมีรายการมากเกินไปโปรดลดและกลับมาในภายหลัง!";
		close;
	}
	if (MaxWeight - Weight < 1000) {
		mes "ตอนนี้คุณมีน้ำหนักเกินโปรดลดและกลับมาในภายหลัง!";
		close;
	}
	mes "[Stone Exchanger]";
	mes "คำอธิบายข้อความเริ่มต้นขึ้น:";
	mes "กด 1 เพื่อยกเลิก";
	mes "กด 2 สำหรับคำอธิบาย";
	mes "กด 3 สำหรับการแลกเปลี่ยนเหรียญ";
	mes "กด 4 สำหรับการแลกเปลี่ยนกล่องใหม่";
	next;
	switch(select("1.ยกเลิก:2.คำอธิบาย:3.แลกเปลี่ยน Stone เป็น Stone Coin:4.แลกเปลี่ยน Stone Coin เป็น BoxStone")) {
	case 1:
		mes "[Stone Exchanger]";
		mes "ยินดี!";
		close;
	case 2:
		mes "[Stone Exchanger]";
		mes "ฉบับที่ 3 เมื่อคุณเลือกการแลกเปลี่ยนเหรียญระบบจะรวบรวม Stone 1 ea จากสินค้าคงคลังของคุณแบบสุ่มเพื่อแลกเปลี่ยนเป็นเหรียญเดียว";
		next;
		mes "[Stone Exchanger]";
		mes "No.4 เมื่อคุณเลือกการแลกเปลี่ยน Stone Coin ระบบจะรวบรวมห้าเหรียญจากสินค้าคงคลังของคุณเพื่อแลกเปลี่ยนเป็น BoxStone แบบสุ่ม";
		next;
		mes "[Stone Exchanger]";
		mes "ยินดี!";
		close;
	case 3:
		specialeffect2 EF_COIN;
		specialeffect2 EF_LOUD;
		progressbar "0xffff00",3;
		callsub L_Exchange,	// yes, this list is hardcoded in the official script...
				6636,6637,6638,6639,6640,6641,6642,6643,6644,6645,
				6716,6717,6718,6740,6741,6742,6743,6744,6745,6790,
				6791,6792,6908,6943,6944,6945,6946,6947,6948,6949,
				6950,6951,6963,6964,6999,25000,25001,25002,25003,25004,
				25005,25006,25007,25008,25009,25010,25011,25012,25013,25014,
				25015,25016,25017,25060,25061,25062,25063,25064,25065,25066,
				25068,25069,25070,25071,25072,25139,25141,25171,25172,25173,
				25174,25175,25227,25228,25229,25304,25305;
		mes "[Stone Exchanger]";
		mes "การค้นหา Stone เสร็จสิ้นแล้ว!";
		next;
		mes "[Stone Exchanger]";
		mes "ขอบคุณที่มา!";
		close;
	case 4:
		specialeffect2 EF_COIN;
		specialeffect2 EF_LOUD;
		progressbar "0xffff00",3;
		if (countitem(6379) < 5) {
			mes "[Stone Exchanger]";
			mes "คุณมีเหรียญไม่เพียงพอ";
			close;
		}
		delitem 6379,5; //Card_Coin
		.@r = rand(1,4);
		     if (.@r == 1)  getitem 4001,1; //Poring_Card
		else if (.@r == 2)  getitem 4002,1; //Fabre_Card
		else if (.@r == 3)  getitem 4002,1; //Fabre_Card
		else getitem 4003,1; //Pupa_Card
		mes "[Stone Exchanger]";
		mes "กล่องทั้งหมดเสร็จแล้ว";
		next;
		mes "[Stone Exchanger]";
		mes "ขอบคุณที่มา!";
		close;
	}
	close;

L_Exchange:
	freeloop(1);
	.@args = getargcount();
	for (.@i = 0; .@i < .@args; .@i++) {
		if (countitem(getarg(.@i))) {
			mes "พบ ^980078" + getitemname(getarg(.@i)) + "^000000,";
			mes "คุณต้องการแลกเปลี่ยน Stone เป็น Stone Coin หรือไม่?";
			next;
			switch(select("ไม่:ใช่")) {
			case 1:
				mes "^980078คุณต้องการค้นหา Stone อื่น ๆ หรือไม่?^000000";
				next;
				if(select("ไม่:ใช่") == 1) {
					mes "^980078การค้นหา Stone ถูกขัดจังหวะ^000000";
					close;
				}
				break;
			case 2:
				if (countitem(getarg(.@i))) {
					delitem getarg(.@i),1;
					getitem 6379,1; //Card_Coin
					mes "^980078" + getitemname(getarg(.@i)) + " ได้แลกเปลี่ยนเป็นเหรียญ^000000";
					next;
				} else {
					mes "^980078สินค้าคงคลังของคุณไม่มี Stone สำหรับการแลกเปลี่ยน^000000";
					next;
				}
				break;
			}
		}
	}
	freeloop(0);
	return;
}
