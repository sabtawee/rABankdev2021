//===== eAthena Script ======================================= 
//= Quest NPCs related to Aldebaran 
//===== By: ================================================== 
//= eAthena Dev Team
//===== Current Version: ===================================== 
//= 1.3
//===== Compatible With: ===================================== 
//= eAthena 7.15 +
//===== Description: ========================================= 
//= 'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quests.
//===== Additional Comments: ================================= 
//= Fully working
//= 1.1 Fixed Doctor Band items [Lupus]
//= 1.2 Removed Duplicates [Silent]
//= 1.3 Updated headgear NPC. [L0ne_W0lf]
//============================================================ 


//=======================================================================================================//
//			'Doctor Band', 'Feather Bonnet', 'Opera Masque', 'Sakkat Hat' Quest
//=======================================================================================================//
aldeba_in,152,166,4	script	Trader#01	86,{
	mes "[Trader]";
	mes "ท่านผู้นี้คือใครกันน่ะ..?";
	mes "ฉัน..คือ นักผลิตสินค้าที่นิยม ๆ ในเมืองแห่งนี้..'";
	next;
	mes "[Trader]";
	mes "ฉันนั้นทำสินค้าเกี่ยวกับหมวกสวมใส่อยู่ที่สถานที่ Rune-Midgard เจ้าสนใจหมวกของข้าไหม? ..";
	next;
	mes "[Trader]";
	mes "สิ่งที่ฉันนั้นทำทั้งหมดเพื่อสร้างและเก็บรวบรวมข้อมูลในการทำธุรกิจของฉัน เอาละคุณชอบหมวกไหนก็เลือกดูละกันแล้วก็ดูสิ่งที่ไปตามหาด้วย...";
	next;
	mes " (1) ^3355FFDoctor Band^000000:";
	mes "1 Red Bandana + 50 Irons + 1 Cracked Diamond + 3500 Zeny";
	mes " (2)^3355FFFeather Bonnet^000000:";
	mes "1 Romantic Gent + 300 Feather of Birds + 500 Zeny";
	mes " (3) ^3355FFPhantom of Opera^000000:";
	mes "20 Iron + 1 Singing Plant + 5000 Zeny ";
	mes " (4) ^3355FFSakkat^000000:";
	mes "120 Trunks + 10000 Zeny ";
	next;
	switch(select(" Doctor Band : Feather Bonnet : Phantom of Opera : Sakkat ")) {
	case 1:
		if ((countitem(2275) > 0) && (countitem(998) > 49) && (countitem(733) > 0) && (Zeny > 3499)) {
			delitem 2275,1;  //Red_Bandana
			delitem 998,50;  //Iron
			delitem 733,1;  //Crystal_Jewel___
			set Zeny,Zeny-3500;
			mes "[Trader]";
			mes "อืม! คุณชอบหมวกนี้หรอ..? หมวกนี้เป็นหมวกที่แพทย์ใช้ในการสวมใส่..เพื่อทำการรักษาผู้ป่วย ท่านมีจิตใจที่..อาสา";
			next;
			mes "[Trader]";
			mes "อ่ะเอานี่ไป..สิ่งที่ท่านต้องการ";
			getitem 2273,1; // Doctor_Cap
			close;
		}
		else {
			mes "[Trader]";
			mes "เห้..คุณ~";
			mes "ช่วยตรวจสอบตัวคุณอีกทีน่ะ.";
			mes "คุณอาจจะนำสิ่งของมาไม่ครบก็เป็นไปได้..";
			close;
		}
	case 2:
		if ((countitem(2247) > 0) && (countitem(916) > 299) && (Zeny > 499)) {
			delitem 2247,1;  //Oldman's_Romance
			delitem 916,300;  //Feather_Of_Birds
			set Zeny,Zeny-500;
			mes "[Trader]";
			mes "โอ้ว! คุณชอบความสวยามของหมวกนี้หรือ ดีน่ะที่คุณมาหาถูกคนแล้ว..เอานี่ไปเลย";
			getitem 5018,1; // Feather_Bonnet
			close;
		}
		else {
			mes "[Trader]";
			mes "เห้..คุณ~";
			mes "ช่วยตรวจสอบตัวคุณอีกทีน่ะ.";
			mes "คุณอาจจะนำสิ่งของมาไม่ครบก็เป็นไปได้..";
			close;
		}
	case 3:
		if ((countitem(998) > 19) && (countitem(707) > 0) && (Zeny > 4999)){ //Iron Singing_Plant 
			delitem 998,20;  //Iron
			delitem 707,1;  //Singing_Plant
			set Zeny,Zeny-5000;
			mes "[Trader]";
			mes "ต้องการสิ่งนี้หรือ..? นี่คือหน้ากาก Phantom of Opera คุณคิดว่ายังไง ? คุณชอบมันไหม? ถ้าใช่เดียวฉันจะนำมาให้..!";
			getitem 2281,1; // Phantom_Of_Opera
			close;
		}
		else {
			mes "[Trader]";
			mes "เห้..คุณ~";
			mes "ช่วยตรวจสอบตัวคุณอีกทีน่ะ.";
			mes "คุณอาจจะนำสิ่งของมาไม่ครบก็เป็นไปได้..";
			close;
		}
	case 4:
		if ((countitem(1019) > 119) && (Zeny > 9999)){ //Wooden_Block 
			delitem 1019,120;  //Wooden_Block
			set Zeny,Zeny-10000;
			mes "[Trader]";
			mes "หมวกใบนี้ฉันได้มาจาก หมู่บ่าน Payon เป็นหมวก Sakkat ของท่าน Craftsman.";
			next;
			mes "[Trader]";
			mes "เป็นสมบัติที่หายากมากกว่าฉันจำทำหมวกได้สนสำเร็จหมดไม้ไผ่..ไปไม่รู้เท่าไหร่แล้ว!";
			getitem 2280,1; // Sahkkat
			close;
		}
		else {
			mes "[Trader]";
			mes "เห้..คุณ~";
			mes "ช่วยตรวจสอบตัวคุณอีกทีน่ะ.";
			mes "คุณอาจจะนำสิ่งของมาไม่ครบก็เป็นไปได้..";
			close;
		}
	}
}
