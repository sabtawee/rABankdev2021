//===== Hercules Script ======================================
//= Blacksmith Skills Quests
//===== By: ==================================================
//= Lupus, Reddozen, Samuray22
//===== Current Version: =====================================
//= 1.6
//===== Description: =========================================
//= [Official Conversion]
//= Quests for skills: Dubious Salesmanship, Greed
//===== Additional Comments: =================================
//= 1.6 Rescripted to Aegis 10.3 stadards. [L0ne_W0lf]
//============================================================

geffen,178,72,3	script	Akkie#qsk_bs	726,{
	if (BaseJob == Job_Blacksmith) {
		if (BLACK_SK == 7 && getskilllv("BS_UNFAIRLYTRICK") == 0) {
			mes "[Akkie]";
			mes " ข้าไม่ได้เห็นท่านมานานเท่าไหร่แล้ว?";
			mes " ครั้งสุดท้ายที่เราพบกันท่านคงลืมสิ่งที่ข้าได้สอนท่านไปหมดแล้ว";
			mes " แต่ไม่เป็นไร.. หลังจากที่ท่านเปลี่ยนอาชีพ ความทรงจำของท่านคงหายไป";
			mes " ข้าจะสอนให้ท่านใหม่เอง~";
			next;
			mes "[Akkie]";
			mes " ที่จริงแล้ว, ข้าแค่เขย่าที่ตัวท่านเบาๆ";
			mes " ความจำของท่านก็น่าฟื้นขึ้นมา";
			mes " คราวนี้ท่านคงจะสามารถใช้งานทักษะ Unfair Trick ได้ทันที..";
			set BLACK_SK,8;
			skill "BS_UNFAIRLYTRICK",1,0;
			close;
		}
		else if (BLACK_SK == 8) {
			mes "[Akkie]";
			mes " สวัสดี อีกครั้ง~";
			mes " ท่านกลับมาพร้อมกลับการเป็นช่างตีเหล็กที่ดี";
			mes " ท่านคงมีอะไรที่ต้องการจะพูดกับข้ารึ~";
			next;
			mes "[Akkie]";
			mes " มันอาจจะน่าเบื่อบ้าง";
			mes " ที่ข้าไปศึกษาเกี่ยวกับทักษะ Unfair Trick";
			mes " จาก Blacksmiths และ Master Smiths";
			mes " ถ้าไม่มีอะไรแล้ว ขอให้ท่านโชคดีเดินทางอย่างปลอดภัย...?";
			close;
		}
		if (JobLevel > 29) {
			if (BLACK_SK == 0) {
				mes "[Akkie]";
				mes " สวัสดี~";
				mes " วันนี้อากาศดีนะ";
				mes " อากาศดีแบบนี้ช่างเหมาะกับ";
				mes " การทำธุรกิจซะจริง";
				mes " หรือจะออกไปล่าสัตว์ดี";
				mes " นั่นก็ไม่เลวเท่าไหร่หรอก";
				next;
				mes "[Akkie]";
				mes " ข้าคือ Akkie ตัวแทนแห่งสมาคม Blacksmith";
				mes " ข้าถูกส่งเรียนรู้ทักษะ Unfair Trick";
				mes " จาก Blacksmiths และ Master Smiths";
				next;
				mes "[Akkie]";
				mes " ข้าจะบอกท่านถึงการเพิ่ม Job Skill ใหม่ๆ";
				mes " - Unfair Trick - ก็คือ";
				mes " - ทักษะที่จำทำให้ -";
				mes " - การเสีย Zeny ในการใช้ Mamonite ลดลง 10% -";
				mes " - เป็นทักษะแบบติดตัว-";
				next;
				mes "[Akkie]";
				mes " ถึงแม้ว่าข้าจะยังเป็นมือใหม่";
				mes " เจ้าสนใจ Unfair Trick บ้างไหม?";
				mes " ข้าสามารถสอนเจ้าได้นะ";
				next;
				if (select(" ข้าจะเรียน: ไม่ ไว้คราวหน้า") == 1) {
					mes "["+ strcharinfo(0) +"]";
					mes " แน่นอน ข้าพยายามที่จะเรียนรู้ทักษะใหม่";
					mes " มันอาจจะเป็นประโยชน์ต่อข้าบ้าง?";
					next;
					mes "[Akkie]";
					mes " โอ้, เยี่ยมไปเลย!";
					mes " ท่านจะไม่เสียใจเลยที่เลือกเช่นนี้!";
					mes " ตอนนี้, ข้าจะอธิบายเกี่ยวกับการใช้งานของทักษะนี้ให้เจ้าได้รู้";
					next;
					mes "[Akkie]";
					mes " การใช้ Unfair Trick ก็คือ";
					mes " ทักษะที่จำทำให้";
					mes " การเสีย Zeny ในการใช้ Mamonite ลดลง 10%";
					mes " โดยการใช้ Zeny ปลอมหลอกศัตรูว่าเป็นสิ่งที่ใช้โจมตีเป็นเงิน Zeny";
					next;
					mes "[Akkie]";
					mes " เอาล่ะ ตอนนี้, ข้าต้องการให้ท่านช่วยนำวัตถุดิบบางอย่าง";
					mes " เพื่อที่ข้าจะสามารถสร้างสิ่งนั้นขึ้นมา";
					mes " สิ่งที่ท่านต้องนำมาก็คือ";
					mes " 1 Steel, 5 Coal,";
					mes " 1 Iron Hammer และ... เอ่อ...";
					next;
					mes "[Akkie]";
					mes " อืม, เดี๋ยว ขอเวลาสักเดี๋ยว";
					mes " ข้าขอดูคู่มือใหม่ให้ดีก่อน";
					mes " ขออภัยด้วยกับเรื่องนี้...";
					mes " ^333333( ข้าคิดว่าข้าจด";
					mes " มันไว้ตรงนี้นะ แล้วมันไปอยู่ไหน?)^000000";
					next;
					mes "[Akkie]";
					mes " โอ้! โอ้, ข้าเจอแล้ว!";
					mes " มันน่าจะใช้นะ ข้าจะบอกเจ้าอีกรอบแล้วกัน";
					mes " เอานะ... อืมม...";
					next;
					mes "["+ strcharinfo(0) +"]";
					mes "............";
					next;
					mes "[Akkie]";
					mes " Steel 1ea, Coal 5 ea, Iron Hammer 1 ea";
					mes " 500 Zeny ไว้ใช้เป็นตัวอย่างของเงิน Zeny";
					mes " และ Detrimindexta 1 ea";
					mes " มันอยู่นี่เอง ฮ่าฮ่าฮ่า";
					next;
					mes "["+ strcharinfo(0) +"]";
					mes " แน่ใจนะ,";
					mes " ว่าสิ่งของเหล่านี้";
					mes " มันจะใช้ในการเรียนทักษะใหม่? ...";
					next;
					mes "[Akkie]";
					mes " แน่ใจสิ, ข้าไม่รู้ว่ามันจะมีเรื่องแบบนี้เกินขึ้น!";
					mes " ตอนนี้ ท่านก็แค่นำวัตถุดิบที่ข้าต้องการ";
					mes " เพื่อพิสูจน์ว่าท่านสามารถเรียนรู็มันได้ง่ายๆ!";
					next;
					mes "[Akkie]";
					mes " ท่านจำเป็นจะต้องเชื่อในตัวข้า!";
					mes " และท่านจะไม่ผิดหวัง,";
					mes " ข้าสัญญา! ว่าทักษะนี้จะเป็นประโยชน์ต่อท่านมาก";
					set BLACK_SK,1;
					close;
				}
				mes "[Akkie]";
				mes " หา อะไร?! แล้วท่านเป็นใคร?";
				mes " ข้าสัญญาว่า ข้าจะทำหน้าที่เป็นครูฝึกให้ดีที่สุด";
				mes " เพือให้ข้าได้เข้าสู่สมาคมของข้าอย่างน่าภูมิใจ";
				close;
			}
			else if (BLACK_SK == 1) {
				if (countitem(999) > 0 && countitem(1003) > 4 && countitem(971) > 0 && countitem(613) > 0) {
					if (Zeny >= 500) {
						mes "[Akkie]";
						mes " โอ้, ท่านมาแล้ว~";
						mes " เอาของที่ท่านพกมาออกมาให้ข้าดูหน่อย";
						next;
						mes "[Akkie]";
						mes " ทำได้ดีมาก, ดูเหมือนตอนนี้";
						mes " ท่านมีสิ่งที่เราต้องการครบแล้ว";
						mes " เอาล่ะ, รอสักครู๋";
						mes " ข้าจะนำวัสดุเหล่านี้ไปใช้งาน พร้อมนะ?";
						delitem 1003,5; //Coal
						delitem 999,1; //Steel
						delitem 971,1; //Detrimindexta
						delitem 613,1; //Iron_Hammer
						set Zeny,Zeny - 500;
						set BLACK_SK,2;
						close;
					}
					else {
						mes "[Akkie]";
						mes " อืม, ท่านมีวัสดุครบหมดแล้ว";
						mes " แต่ท่านยังขาดเงิน 500 Zeny";
						mes " เพื่อที่เราจะสามารถเลียนแบบของจริงได้อย่างสมบูรณ์";
						close;
					}
				}
				else {
					mes "[Akkie]";
					mes " โอ้, ท่านมาแล้ว~";
					mes " เอาของที่ท่านพกมาออกมาให้ข้าดูหน่อย";
					next;
					mes "[Akkie]";
					mes " หืมม์, ข้าว่ามีอย่างที่ยังขาดหายไปอยู่นะ";
					mes " ข้าจะบอกสื่งที่ข้าต้องการให้ท่านอีกครั้งแล้วกัน";
					mes " มันจำเป็นมากสำหรับทักษะนี้";
					next;
					mes "[Akkie]";
					mes " Steel 1ea, Coal 5 ea, Iron Hammer 1 ea";
					mes " 500 Zeny ไว้ใช้เป็นตัวอย่างของเงิน Zeny";
					mes " และ Detrimindexta 1 ea";
					mes " ท่านคงไม่ลืมมันอีกนะ?";
					close;
				}
			}
			else if (BLACK_SK == 2) {
				mes "[Akkie]";
				mes " อ้า! ท่านกลับมาเร็วกว่าที่ข้าคิด";
				mes " หือ ข้าควรทำอย่างไรดี บอกท่านเลยดีไหม?";
				mes " ";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "...";
				mes "......";
				mes ".........";
				next;
				mes "[Akkie]";
				mes " ข้าขอโทษ! ข้าสับสน";
				mes " มีบางอย่างเกิดขึ้น";
				mes " ในขณะที่ข้าทำตัวอย่างการลอกเลียนแบบ!";
				mes " ได้โปรดอย่านำเรื่องนี้ไปบอกกับสมาคม Blacksmith";
				mes " ให้โอกาสข้าได้แก้ตัวเถอะนะ!";
				next;
				mes "[Akkie]";
				mes " ได้โปรดเถิด ได้โปรด";
				mes " ได้โปรดให้โอกาสให้ข้าอีกสักครั้ง";
				mes " Steel 1 ea และ Coals 3 ea!";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes " เอาล่ะ, ตกลง ตกลง...";
				mes " ท่านแน่ใจนะ";
				mes " ทั้งนั้นคือสิ่งที่ท่านต้องการ?";
				next;
				mes "[Akkie]";
				mes " ใช่, แน่นอน! โอ้, ขอบคุณ";
				mes " ท่านช่างเป็นคนใจกว้างมากๆ!";
				mes " ท่านช่วยนำ";
				mes " Steel 1 ea และ Coals 3 ea!";
				mes " กลับมาให้ข้าอีกครั้ง";
				set BLACK_SK,3;
				close;
			}
			else if (BLACK_SK == 3) {
				if (countitem(999) > 0 && countitem(1003) > 2) {
					mes "[Akkie]";
					mes " โอ้, ขอบคุณมาก";
					mes " สำหรับสิ่งเหล่านี้";
					mes " ข้าจะพยายามทำมันให้สำเร็จ";
					mes " ให้เวลาข้าเตรียมความพร้อมสักเดี๋ยว....";
					delitem 999,1; //Steel
					delitem 1003,3; //Coal
					set BLACK_SK,4;
					close;
				}
				else {
					mes "[Akkie]";
					mes " ท่านช่วยนำ";
					mes " Steel 1 ea และ Coals 3 ea!";
					mes " กลับมาให้ข้าอีกครั้ง";
					close;
				}
			}
			else if (BLACK_SK == 4) {
				mes "[Akkie]";
				mes "......";
				mes ".........";
				mes "............";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes "...?";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes " โอ้ ไม่...";
				mes " ไม่... ไม่นะ...";
				mes " มีบางอย่างเกิดขึ้น?";
				next;
				mes "[Akkie]";
				mes " *Sob* sob~";
				mes " ระหว่างที่ข้ากำลังทำมันอยู่ Iron Hammer เกิดหักน่ะ";
				mes " มันพังไปซะแล้ว~";
				mes " sob sob~~~~";
				next;
				mes "["+ strcharinfo(0) +"]";
				mes " ^333333( ข้าเดาว่า ข้าต้องหาของ";
				mes " ของใหม่มาให้เธออีก";
				mes " นำ ^FF0000Iron Hammer^333333";
				mes " มาและนำไปให้เธอ!)^000000";
				set BLACK_SK,5;
				close;
			}
			else if (BLACK_SK == 5) {
				if (countitem(613) > 0) {
					mes "[Akkie]";
					mes " นี่คือ Iron Hammer สำหรับข้าใช่ไหม?";
					mes " ขอบคุณมากสำหรับการช่วยเหลือ...";
					next;
					mes "[Akkie]";
					mes " โอเค, ท่านน่าจะให้ข้าลองพยายามดูอีกสักครั้งหนึ่ง?";
					delitem 613,1; //Iron_Hammer
					set BLACK_SK,6;
					close;
				}
				else {
					mes "[Akkie]";
					mes " *Sob* sob~";
					mes " ระหว่างที่ข้ากำลังทำมันอยู่ Iron Hammer เกิดหักน่ะ";
					mes " มันพังไปซะแล้ว~";
					mes " นำ Iron Hammer มาและนำไปให้เธอ";
					close;
				}
			}
			else if (BLACK_SK == 6) {
				mes "[Akkie]";
				mes " โอ้, ท่านกลับมาแล้ว!";
				mes " ในที่สุดข้าก็ทำได้สำเร็จ!";
				mes " ข้าได้ทดสอบกับ Mammonite แล้วมันทำได้ยอดเยี่ยม!";
				next;
				mes "[Akkie]";
				mes " โอ้, ก่อนที่ท่านจะได้เรียนรู้ทักษะใหม่";
				mes " มีเพื่อนร่วมงานของข้า";
				mes " เขารู็ทักษะอีกหนึ่งชนิด เรียกว่า Greed";
				mes " หากท่านสนใจให้ท่านไปพบ Goodman เขาจะเป็นคนสนท่านเอง";
				next;
				mes "[Akkie]";
				mes " เอาหล่ะ, ตอนนี้คือการเรียนรู้ Unfair Trick";
				mes " คือการใช้ Zeny ปลอมหลอกล่อศัตรู";
				mes " ท่านจะสามารถลดค่าใช้จ่ายจากทักษะ Mamonite...";
				set BLACK_SK,7;
				skill "BS_UNFAIRLYTRICK",1,0;
				next;
				mes "[Akkie]";
				mes " ดังนั้น, ท่านจะสามารถใช้ Mammonite โดยเสีย Zeny เพียง 90% เท่านั้น";
				mes " และนี่คือทั้งหมดเกี่ยวกับ ทักษะ Unfair Trick!";
				next;
				mes "[Akkie]";
				mes " ขอบคุณท่านอีกครั้ง";
				mes " สำหรับความผิดพลาดของข้า";
				mes " ที่มันทำให้ท่านต้องรำคาญ";
				mes " แต่ข้าเชื่อว่าทักษะนี้";
				mes " มันจะเป็นประโยชน์สำหรับท่าน";
				close;
			}
			else {
				if (rand(1,2) == 1) {
					mes "[Akkie]";
					mes " โอ้, ท่านได้ไปพบกับ";
					//mes "Goodman in Einbroch yet?";
					mes " Goodman ใน Geffen หรือยัง?";
					mes " เขาเป็นตัวแทนแห่ง";
					mes " สมาคม Blacksmith ";
					mes " เขาสอนทักษะ ที่มีชื่อว่า Greed";
					close;
				}
				else {
					mes "[Akkie]";
					mes " สวัสดี อีกครั้ง~";
					mes " ท่านกลับมาพร้อมกลับการเป็นช่างตีเหล็กที่ดี";
					mes " ท่านคงมีอะไรที่ต้องการจะพูดกับข้ารึ~";
					next;
					mes "[Akkie]";
					mes " มันอาจจะน่าเบื่อบ้าง";
					mes " ที่ข้าไปศึกษาเกี่ยวกับทักษะ Unfair Trick";
					mes " จาก Blacksmiths และ Master Smiths";
					mes " ถ้าไม่มีอะไรแล้ว ขอให้ท่านโชคดีเดินทางอย่างปลอดภัย...?";
					close;
				}
			}
		}
		else {
			mes "[Akkie]";
			if (Upper == 1) {
				mes " สวัสดี, ท่าน "+ strcharinfo(0) +",";
				mes " ข้าอยู่ที่นี่ในนามของ";
				mes " สมาคม Blacksmith ข้ามีหน้าที่สอนทักษะใหม่";
				mes " แต่ท่านยังเด็กเกินไป";
				mes " ข้าคงยังสอนอะไรให้กับท่านไม่ได้...";
				next;
				mes "[Akkie]";
				mes " เมื่อท่านเติบโตขึ้น";
				mes " แน่นอน, ข้าจะสอนทักษะใหม่นี้ให้กับเจ้าเอง!";
				mes " ลาก่อน โชคดี~";
			}
			else {
				mes " ข้าคือ Akkie จาก";
				mes " สมาคม Blacksmith";
				mes " ข้ามีหน้าที่สอนทักษะใหม่";
				mes " ที่ข้าไปศึกษามาจาก Blacksmiths";
				mes " และ Master Smiths แต่ดูเหมือน";
				mes " ตอนนี้จ๊อบของเจ้ายังต่ำเกินไป...";
				next;
				mes "[Akkie]";
				mes " "+ strcharinfo(0) +", ข้าต้องเสียใจกับท่่านด้วย";
				mes " แต่ท่านจะได้เรียนทักษะนี้";
				mes " หลังจากที่ระดับจ๊อบของท่านสูงขึ้น";
			}
			close;
		}
	}
	else {
		mes "[Akkie]";
		mes " สวัสดี~";
		mes " วันนี้อากาศดีนะ";
		mes " อากาศดีแบบนี้ช่างเหมาะกับ";
		mes " การทำธุรกิจซะจริง";
		mes " หรือจะออกไปล่าสัตว์ดี";
		mes " นั่นก็ไม่เลวเท่าไหร่หรอก";
		close;
	}
}

geffen,172,52,1	script	Goodman#qsk_bs	826,{
	if (BaseJob == Job_Blacksmith) {
		if (BLACK_SK2 == 2 && Upper == 1 && getskilllv("BS_GREED") == 0) {
			mes "[Goodman]";
			mes " อ้า, ข้าจำท่านได้";
			mes " ข้าเคยเห็นท่านตั้งแต่ยังเป็น Black Smiths";
			mes " แต่ตอนนี้ท่านเปลี่ยนเป็น Master Smiths แล้ว...";
			next;
			mes "[Goodman]";
			mes " หืมม์? ท่านคงลืมทักษะ Greed ไปแล้วใช่ไหม?";
			mes " ที่จริงแล้ว, ข้าแค่เขย่าที่ตัวท่านเบาๆ";
			mes " ความจำของท่านก็น่าฟื้นขึ้นมา";
			mes " คราวนี้ท่านคงจะจำทักษะ Greed ได้แล้วหล่ะ..";
			skill "BS_GREED",1,0;
			set BLACK_SK2,3;
			close;
		}
		else if (BLACK_SK2 == 3) {
			mes "[Goodman]";
			mes " เทคนิคการตีอาวุธนั้นมีมานานมากแล้ว";
			mes " เป็นโอกาสที่ดี หากท่านลองเดินทางไปที่ต่างๆ";
			mes " เพื่อตามหาวัสดุ เพื่อนำมาตีอาวุธ";
			mes " มันคงจะทำให้ท่านได้รู้สึกผ่อนคลาย";
			close;
		}
		else {
			if (BLACK_SK2 == 0) {
				mes "[Goodman]";
				mes " ข้าเป็นหนึ่งในสมาคม Blacksmith";
				mes " ที่มีหน้าที่รับผิดชอบในส่วนของการผ่านทักษะ";
				mes " ข้าคือ Mr. Goodday";
				mes " ผู้คนมักจะเรียกข้าว่า Goodman";
				next;
				mes "[Goodman]";
				mes " เป็นข่าวดี, ที่ข้าอยู่ในการดูแล";
				mes " ของการศึกษาและพัฒนาเกี่ยวกับทักษะใหม่ๆ";
				mes " สำหรับ Blacksmith และ Merchant";
				mes " พวกเขาเรียกมัน ^FF0000Greed^000000";
				mes " มันเป็นทางเลือกที่ดีหากท่านสนใจจะเรียนรู้มัน";
				next;
				mes "[Goodman]";
				mes " ถ้าท่านอยากจะเรียน";
				mes " ข้าจะแนะนำท่านเกี่ยวกับพื้นฐาน";
				mes " และวิธีการใช้งานของมันก่อน";
				mes " ท่านว่ายังไงล่ะ?";
				next;
				if (select(" ข้าจะเรียน!: คราวหน้าแล้วกัน~") == 1) {
					mes "[Goodman]";
					mes " เฮ้! ท่านเลือกได้ดี,";
					mes " ทักษะ Greed มีความสามารถ";
					mes " เก็บไอเท็มอัติโนมัติในพื้น 5*5";
					mes " ในระยะที่มีการร่าย";
					mes " มันประโยชน์มากสำหรับการป้องกันการขโมยจากมอนสเตอร์";
					next;
					mes "[Goodman]";
					mes " ขั้นแรก, ข้าต้องทดสอบดูว่า";
					mes " ท่านมีคุณสมบัติเพียงพอ";
					mes " ที่จะเรียนรู้ทักษะนี้่ได้หรือไม่";
					next;
					mes "[Goodman]";
					mes " จากนี้ ท่านจะต้องออกไป";
					mes " - รวบรวมสิ่งของ ของสวมใส่ หรือโพชั้นที่ท่านพอจะหาได้ -";
					mes " - ไม่สนใจว่าปริมาณหรือชนิดของไอเท็ม -";
					mes " - ข้าแค่ให้ท่าน -";
					mes " - รวบรวมทุกสิ่ง อะไรก็ได้ -";
					mes " - ที่ทำให้ท่านมีน้ำหนัก 500 -";
					mes " - นำทุกสิ่งใส่ในช่องเก็บของๆ ท่าน -";
					mes " - เสร็จแล้วให้มาพบข้า -";
					set BLACK_SK2,1;
					close;
				}
				mes "[Goodman]";
				mes " ข้าเห็นแล้ว, ว่ากำลังยุ่งอยู่กับสิ่งอื่น";
				mes " แต่ของมีค่าอื่นๆ ท่านควรจะใช้มันอย่างคุ้มค่ามากที่สุด";
				mes " เข้าใจไหม?";
				close;
			}
			else if (BLACK_SK2 == 1) {
				if (MaxWeight - Weight < 5000) {
					mes "[Goodman]";
					mes " กลับมาแล้วรึ?";
					mes " เอาล่ะ, มาให้ข้าตรวจสอบ";
					mes " สิ่งของภายในตัวท่าน";
					mes " ดูซิ, Inventory, Inventory... ^FFFFFF ^000000";
					next;
					mes "[Goodman]";
					mes " ทำได้ดีมาก ท่านผ่านการทดสอบ";
					mes " ท่านควรนึกถึงว่าเวลาและประสบกาณ์ที่ใช้ในการรวบรวมไอเท็มเหล่านี้";
					mes " มีมูลค่าราคาของไอเท็ม ที่ท่านนำมาอีกใช่ไหม?";
					next;
					mes "[Goodman]";
					if (Upper == 1)
						mes " ข้าเชื่อใน Master Smiths";
					else
						mes " ข้าเชื่อใน Blacksmiths";
					mes " สิ่งสำคัญที่สุดไม่ใช่ความร่ำรวยและชื่อเสียง";
					mes " แต่สิ่งสำคัญที่สุดนั่นก็คือฝีมือและคุณภาพที่ท่านได้ทำ";
					if (Upper == 1)
						mes " Master Smiths อย่างเราควรรู้ ^FFFFFF ^000000 เกียรติยศจะอยู่ในการทำงานของพวกเรา...";
					else
						mes " Blacksmiths อย่างเราควรรู้ ^FFFFFF ^000000 เกียรติยศจะอยู่ในการทำงานของพวกเรา...";
					next;
					if (Upper == 1) {
						mes "[Goodman]";
						mes " หากท่าน, ไม่ได้ให้ความสำคัญไอเท็มในเรื่องราคาของมันมากเกินไป";
						mes " ท่านอาจจะกลายเป็นคนรวยได้แบบไม่รู้ตัว";
						mes " ดูที่ข้าทำสิ ไม่ยากอะไรใช่ไหม?";
						mes " ทำไมเจ้าไม่ลองทำมันดูบ้างล่ะ?";
						next;
					}
					mes "[Goodman]";
					mes " ทักษะนี้มีชื่อเรียกว่า Greed";
					mes " ข้าหวังว่าท่านจะนำมันไปใช้อย่างถูกต้อง";
					mes " และข้ายังเชื่อว่าสักวันท่านต้องสร้างงานชิ้นเอกได้อย่างแน่นอน";
					skill "BS_GREED",1,0;
					set BLACK_SK2,2;
					close;
				}
				else {
					mes "[Goodman]";
					mes " หืมม์ ท่านกลับมาแล้ว";
					mes " มันยังเร็วเกินไป";
					mes " ยังไม่เรียบร้อย";
					mes " ตั้งใจฟังที่ข้าพูด ข้าจะบอกท่านอีกครั้ง ^FFFFFF ^000000";
					next;
					mes "[Goodman]";
					mes " จากนี้ ท่านจะต้องออกไป";
					mes " - รวบรวมสิ่งของ ของสวมใส่ หรือโพชั้นที่ท่านพอจะหาได้ -";
					mes " - ไม่สนใจว่าปริมาณหรือชนิดของไอเท็ม -";
					mes " - ข้าแค่ให้ท่าน -";
					mes " - รวบรวมทุกสิ่ง อะไรก็ได้ -";
					mes " - ที่ทำให้ท่านมีน้ำหนัก 500 -";
					mes " - นำทุกสิ่งใส่ในช่องเก็บของๆ ท่าน -";
					mes " - เสร็จแล้วให้มาพบข้า -";
					close;
				}
			}
			else {
				mes "[Goodman]";
				mes " ท่านเคยใช้ทักษะ Greed รึยัง";
				mes " ข้าหวังว่ามันจะเป็นประโยชน์ต่อท่าน";
				mes " สำหรับการค้นหาไอเท็มและวัสดุ";
				close;
			}
		}
	}
	mes "[Goodman]";
	mes " ข้ารู้สึกอยากจะสำรวจ";
	mes " ที่ภูเขาแห่งหนึ่งเพื่อมองหา";
	mes " ไอเท็มหายากที่น่าสนใจ";
	mes " ที่ข้าจะนำมาสกัดเป็นไอเท็มของข้า";
	close;
}
